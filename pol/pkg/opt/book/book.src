//////////////////////////////////////////////////
//
//   Shinigami's Book-Package v1.01 (2000/02/26)
//
//     Usage: DoubleClick a Book (0xff0)
//
//     Author: Shinigami
//
//   Dependencies: gumputil.inc
//
//////////////////////////////////////////////////

/////////
// ToDo
/////////
//
// Fuer die 3 Fonts... alle breiten raussuchen und dann beim buch quasi
//   'n konfigbutton als globalprop setzenbar, welcherdann immer von der
//   include genommen wird, ganz egal wofuer
// New gump's for the new Books (0xfef..0xff4)
//   (only normal and golden book's are implemented yet)
// Non-Writing-Books enable
// Lesezeichen
// Seitenzahlen
// Zu lange Woerter verkuerzen ?
// Transform to Myrahti's Gump-Include...
// Optimization
// Documentation

/////////////////
// Bibliotheken
/////////////////

Use uo;
Use os;

/////////////
// Includes
/////////////

Include "include/gumputil";

////////////////////////
// Klassendefinitionen
////////////////////////

Var book:=struct;            // Buch
       book.+gump:=0;        //   Nummer des Gumps
       book.+edge_l_gump:=0; //   Ecke links  -> Nummer des Gumps
       book.+edge_r_gump:=0; //   Ecke rechts -> Nummer des Gumps
       book.+edge_l_x:=0;    //   Ecke links  -> X-Koordinate
       book.+edge_r_x:=0;    //   Ecke rechts -> X-Koordinate
       book.+edge_y:=0;      //   Ecken       -> Y-Koordinate
       book.+line_l_x:=0;    //   Zeile links  -> Basis X-Koordinate
       book.+line_r_x:=0;    //   Zeile rechts -> Basis X-Koordinate
       book.+line_y:=0;      //   Zeilen       -> Basis Y-Koordinate
       book.+width:=0;       //   Zeilen       -> Laenge einer Zeile
       book.+lines:=0;       //   Zeilen       -> Anzahl pro Seite
       book.+textcolor:=0;   //   Textfarbe
       book.+showtitle:=0;   //   Titel anzeigen ?
       book.+title_x:=0;     //   Titel -> X-Koordinate
       book.+title_y:=0;     //   Titel -> Y-Koordinate
       book.+wrbl:={ };      //   Button fuer Schreibmodus
       book.+pgnr:={ };      //   Seitennummern
       book.+sign:={ };      //   Lesezeichen

Var wrbl:=struct;            // Button fuer Schreibmodus
       wrbl.+writeable:=0;   //   Beschreibar ?
       wrbl.+gump:=0;        //   Nummer des Gumps
       wrbl.+edge_l_x:=0;    //   Ecke links  -> X-Koordinate
       wrbl.+edge_r_x:=0;    //   Ecke rechts -> X-Koordinate
       wrbl.+edge_y:=0;      //   Ecken       -> Y-Koordinate

Var pgnr:=struct;            // Seitennummern
       pgnr.+enabled:=0;     //   Nummer anzeigen ?
       pgnr.+gumpbase:=0;    //   Nummer des ersten Gumps
       pgnr.+edge_l_x:=0;    //   Ecke links  -> X-Koordinate
       pgnr.+edge_r_x:=0;    //   Ecke rechts -> X-Koordinate
       pgnr.+edge_y:=0;      //   Ecken       -> Y-Koordinate
       pgnr.+textcolor:=0;   //   Textfarbe
       pgnr.+texture:=0;     //   Texture zum Uebermalen
       pgnr.+nice:=0;        //   Nummer mit Rand drumrum ?

Var sign:=struct;            // Lesezeichen
       sign.+gump:=0;        //   Nummer des Gumps
       sign.+x:=0;           //   X-Koordinate
       sign.+y:=0;           //   Y-Koordinate
       sign.+page:=0;        //   Seite            (0=wird nicht angezeigt)

Var content:=struct;              // Buchinhalt
       content.+titleauthor:={ }; //   Titel und Autor
       content.+story:="";        //   Inhalt

///////////////////////
// Objektdefinitionen
///////////////////////

Var book_wrbl:=wrbl;              // Button fuer Schreibmodus
       book_wrbl.writeable:=1;       //   Beschreibar ?
       book_wrbl.edge_l_x:=177;      //   Ecke links  -> X-Koordinate
       book_wrbl.edge_r_x:=230;      //   Ecke rechts -> X-Koordinate
       book_wrbl.edge_y:=10;         //   Ecken       -> Y-Koordinate

Var golden_book:=book;            // Goldenes Buch
       golden_book.gump:=500;        //   Nummer des Gumps
       golden_book.edge_l_gump:=501; //   Ecke links  -> Nummer des Gumps
       golden_book.edge_r_gump:=502; //   Ecke rechts -> Nummer des Gumps
       golden_book.edge_l_x:=0;      //   Ecke links  -> X-Koordinate
       golden_book.edge_r_x:=356;    //   Ecke rechts -> X-Koordinate
       golden_book.edge_y:=0;        //   Ecken       -> Y-Koordinate
       golden_book.line_l_x:=40;     //   Zeile links  -> Basis X-Koordinate
       golden_book.line_r_x:=230;    //   Zeile rechts -> Basis X-Koordinate
       golden_book.line_y:=30;       //   Zeilen       -> Basis Y-Koordinate
       golden_book.width:=150;       //   Zeilen       -> Laenge einer Zeile
       golden_book.lines:=9;         //   Zeilen       -> Anzahl pro Seite
       golden_book.showtitle:=1;     //   Titel anzeigen ?
       golden_book.title_x:=-125;    //   Titel -> X-Koordinate
       golden_book.title_y:=-120;    //   Titel -> Y-Koordinate
       golden_book.wrbl:=book_wrbl;  //   Button fuer Schreibmodus

Var normal_book:=golden_book;     // Normales Buch
       normal_book.gump:=510;        //   Nummer des Gumps
       normal_book.edge_l_gump:=511; //   Ecke links  -> Nummer des Gumps
       normal_book.edge_r_gump:=512; //   Ecke rechts -> Nummer des Gumps

Var roll:=struct;                   // Schriftrolle
       roll.+gumpbase:=2520;        //   Nummer des ersten Gumps
       roll.+gump_width:=600;       //   Gump -> Breite
       roll.+gump_height:=400;      //   Gump -> Hoehe
       roll.+bttn_x:=40;            //   Buttons -> X-Koordinate
       roll.+bttn_y_refresh:=13;    //   Buttons -> Y-Koordinaten
       roll.+bttn_y_morelines:=roll.gump_height-29;
       roll.+title_x:=-135;         //   Titel -> X-Koordinate
       roll.+title_y:=-95;          //   Titel -> Y-Koordinate
       roll.+line_gumpbase:=3000;   //   Zeilen -> Nummer des ersten Gumps
       roll.+line_x:=37;            //   Zeilen -> Basis X-Koordinate
       roll.+line_y:=39;            //   Zeilen -> Basis Y-Koordinate
       roll.+textcolor:=0;          //   Textfarbe

///////////////
// Konstanten
///////////////

Const bttn_write:=1;     // Button fuer Schreibmodus gedrueckt
Const bttn_refresh:=2;   // Button fuer Neuaufbau gedrueckt
Const bttn_morelines:=3; // Button fuer mehr Zeilen gedrueckt

/////////////////////
// Buchdefinitionen
/////////////////////

Var empty:=content;
       empty.titleauthor:={"", "", "", "", "", ""};
       empty.story:="";

/////////////
// Programm
/////////////

Program Book(who, book)
  If (who.size()) // Wurde das Script kuenstlich aufgerufen?
    book:=who[2];
    who:=who[1];
  EndIf

  Var cont:=content;

  Set_Script_Option(SCRIPTOPT_NO_RUNAWAY, 1);

  Var bookcont:=GetObjProperty(book, "bookcontent");
  If (bookcont)
  	If (TypeOfInt(bookcont)==OT_ARRAY)
  		cont.titleauthor:=bookcont[1];
      cont.story:=bookcont[2];
    Else
      cont.titleauthor:=bookcont.titleauthor;
      cont.story:=bookcont.story;
    EndIf
  Else
    cont:=empty;
    cont.titleauthor[6]:=who.name;

    // Import the Content of Dundee's Books
    Var booktext:=GetObjProperty(book, "booktext");
    If (booktext)
      cont.story:=booktext[1];

      Var opt:=Len(booktext);
      Var i;
      For (i:=2; i<=opt; i+=1);
        cont.story:=cont.story+" "+booktext[i];
      EndFor

      EraseObjProperty(book, "booktext");
    EndIf
  EndIf

  Var bookstyle;
  If ((book.objtype==4079) Or (book.objtype==4081))
    bookstyle:=golden_book;
  Else
    bookstyle:=normal_book;
  EndIf

  If (GetObjProperty(book, "writeable")==0)
    bookstyle.wrbl.writeable:=0;
  Else
    bookstyle.wrbl.writeable:=1;
  EndIf

  cont:=Show_Book(who, bookstyle, cont, 10, 10);

  book.name:=Get_Bookname(cont.titleauthor);
  SetObjProperty(book, "bookcontent", cont);
EndProgram

//////////////////////
// Ein Buch anzeigen
//////////////////////

Function Show_Book(who, book, content, x, y)
  Var bttn;

  Var cont;
  Var layout;
  Repeat
    cont:=Create_BookContent(book, content);
    layout:=Create_BookLayout(book, cont.data, x, y, cont.pages);

   // SysLog("!!! FICKEN - 5 !!! = ["+layout+"]");
   // SysLog("!!! FICKEN - 6 !!! = ["+cont.data+"]");

    bttn:=CInt(SendDialogGump(who, layout, cont.data)[0]);
    If (bttn==bttn_write)

     // SysLog("!!! FICKEN - 7 !!! = ["+content+"]");

      content:=Show_Roll(who, roll, content, x, y);
    EndIf
  Until (bttn<>bttn_write);

  Return (content);
EndFunction

//////////////////////////////////////
// Inhalt des Buches zusammenbasteln
//////////////////////////////////////

Function Create_BookContent(book, content)
  Var cont:=struct;
  Var width:=0;
  Var index:=1;

  cont.+words:=SplitWords(content.story);
  cont.+data:=content.titleauthor;
  cont.+lines:=0;

  While (index<=Len(cont.words))
    If (width==0)
      Var testword:=cont.words[index];

      cont.lines:=cont.lines+1;
      cont.data[cont.lines+6]:=testword;
      width:=GumpTextWidthFast(testword);
      index+=1;
    Else
      Var testword:=" "+cont.words[index];
      Var testwidth:=GumpTextWidthFast(testword);

      If (width+testwidth<book.width)
        cont.data[cont.lines+6]:=cont.data[cont.lines+6]+testword;
        width+=testwidth;
        index+=1;
      Else
        width:=0;
      EndIf
    EndIf
  EndWhile

  cont.+pages:=CInt(cont.lines/book.lines);
  If (cont.pages*book.lines<cont.lines)
    cont.pages:=cont.pages+1;
  EndIf

  If (cont.pages==0)
    cont.pages:=1;
  EndIf

  Var maxlines:=cont.pages*book.lines+6;
  Var line:=cont.lines+7;
  While (line<=maxlines)
    cont.data[line]:="";
    line+=1;
  EndWhile

  Return (cont);
EndFunction

//////////////////////////////////////
// Layout des Buches zusammenbasteln
//////////////////////////////////////

Function Create_BookLayout(book, data, x, y, pages)
  Var layout:={"page 0", "nodispose"};
  Var realpages;
  Var dblpages;
  Var odd:=0;

  layout.append("gumppic "+CStr(x)+" "+CStr(y)+" "+CStr(book.gump));

  If (book.showtitle==1)
    realpages:=pages+1;
  Else
    realpages:=pages;
  EndIf
  dblpages:=CInt(realpages/2);
  If (dblpages*2<realpages)
    dblpages+=1;
    odd:=1;
  EndIf

  Var i;
  For (i:=1; i<=dblpages; i+=1)
    layout:=Create_Layout_Page(book, data, x, y, dblpages, i, odd, layout);
  EndFor

  Return (layout);
EndFunction

Function Create_Layout_Page(book, data, x, y, pages, page, odd, layout)
  Var edge_l_gump:=CStr(book.edge_l_gump)+" ";
  Var edge_r_gump:=CStr(book.edge_r_gump)+" ";
  Var index:=(page-1)*2*book.lines+6;

  layout.append("page "+CStr(page));

  If (pages>1)
    If (page==1)
      layout.append("button "+CStr(x+book.edge_r_x)+" "+
        CStr(y+book.edge_y)+" "+edge_r_gump+edge_r_gump+"0 2");
    ElseIf (page==pages)
      layout.append("button "+CStr(x+book.edge_l_x)+" "+
        CStr(y+book.edge_y)+" "+edge_l_gump+edge_l_gump+"0 "+CStr(page-1));
    Else
      layout.append("button "+CStr(x+book.edge_r_x)+" "+
        CStr(y+book.edge_y)+" "+edge_r_gump+edge_r_gump+"0 "+CStr(page+1));
      layout.append("button "+CStr(x+book.edge_l_x)+" "+
        CStr(y+book.edge_y)+" "+edge_l_gump+edge_l_gump+"0 "+CStr(page-1));
    EndIf
  EndIf

  If (book.showtitle==1)
    index-=book.lines;
  EndIf

  If ((page==1) And (book.showtitle==1))
    layout.append("gumppic "+CStr(x+book.title_x)+" "+
      CStr(y+book.title_y)+" 2204");

    Var i;
    For (i:=1; i<=3; i:=i+1)
      layout.append("text "+CStr(GumpTextMid(data[i+1], x+book.title_x+197,
        90))+" "+CStr(y+book.title_y+158+i*20)+" 50 "+CStr(i));
    EndFor
    For (i:=4; i<=5; i:=i+1)
      layout.append("text "+CStr(GumpTextMid(data[i+1], x+book.title_x+197,
        90))+" "+CStr(y+book.title_y+163+i*20)+" 50 "+CStr(i));
    EndFor
  Else
    Var i;
    For (i:=0; i<book.lines; i:=i+1)
      layout.append("text "+CStr(x+book.line_l_x)+" "+
        CStr(y+book.line_y+i*20)+" "+CStr(book.textcolor)+" "+CStr(index+i));
    EndFor
    If (book.wrbl.writeable==1)
      layout.append("button "+CStr(x+book.wrbl.edge_l_x)+" "+
        CStr(y+book.wrbl.edge_y)+" 4033 4033 1 0 "+CStr(bttn_write));
    EndIf
  EndIf
  If ((page<pages) Or (odd==0))
    Var i;
    For (i:=0; i<book.lines; i:=i+1)
      layout.append("text "+CStr(x+book.line_r_x)+" "+
        CStr(y+book.line_y+i*20)+" "+CStr(book.textcolor)+" "+
        CStr(index+i+book.lines));
    EndFor
    If (book.wrbl.writeable==1)
      layout.append("button "+CStr(x+book.wrbl.edge_r_x)+" "+
        CStr(y+book.wrbl.edge_y)+" 4033 4033 1 0 "+CStr(bttn_write));
    EndIf
  EndIf

  Return (layout);
EndFunction

//////////////////////////
// Buchname zurueckgeben
//////////////////////////

Function Get_Bookname(titleauthor)
  Var words:=SplitWords(titleauthor[2]+" "+titleauthor[3]+" "+titleauthor[4]);
  Var bookname;

  If (Len(words)>0)
    bookname:=words[1];

    Var opt:=Len(words);
    Var i;
    For (i:=2; i<=opt; i+=1);
      bookname:=bookname+" "+words[i];
    EndFor
  Else
    bookname:="ein Buch";
  EndIf

  Return (bookname);
EndFunction

///////////////////////////////
// Eine Schriftrolle anzeigen
///////////////////////////////

Function Show_Roll(who, roll, content, x, y)
  Var newlines:=10;
  Var buttons:=2;
  Var bttn;

  roll.+width:=roll.gump_width-2*roll.line_x-12-16;
  roll.+lines:=CInt((roll.gump_height-2*roll.line_y)/20);

  Var cont;
  Var layout;
  Var result;
  Repeat
    cont:=Create_RollContent(roll, content, newlines, buttons);
    layout:=Create_RollLayout(roll, cont.data, x, y, cont.pages, buttons);

   // SysLog("!!! FICKEN - 2 !!! = ["+layout+"]");
   // SysLog("!!! FICKEN - 3 !!! = ["+cont.data+"]");

    result:=SendDialogGump(who, layout, cont.data);

   // SysLog("!!! FICKEN - 4 !!! = ["+result+"] ("+cont.lines+")");

    content:=Get_Content(result, cont.lines, buttons);

    bttn:=CInt(result[0]);
    If (bttn==bttn_morelines)
      newlines+=10;
    EndIf
  Until ((bttn<>bttn_refresh) And (bttn<>bttn_morelines));

  Return (content);
EndFunction

////////////////////////////////////////////
// Inhalt der Schriftrolle zusammenbasteln
////////////////////////////////////////////

Function Create_RollContent(roll, content, newlines, buttons)
  Var inputwidth:=CInt(roll.width/1.5);
  Var cont:=struct;
  Var width:=0;
  Var index:=1;
  Var i;

  cont.+words:=SplitWords(content.story);
  cont.+data:={"", "Refresh Content", "Add 10 Lines"};
  For (i:=2; i<=6; i:=i+1)
    cont.data[i+buttons]:=content.titleauthor[i];
  EndFor
  cont.+lines:=0;

  While (index<=Len(cont.words))
    If (width==0)
      Var testword:=cont.words[index];

      cont.lines:=cont.lines+1;
      cont.data[cont.lines+6+buttons]:=testword;
      width:=GumpTextWidthFast(testword);
      index+=1;
    Else
      Var testword:=" "+cont.words[index];
      Var testwidth:=GumpTextWidthFast(testword);

      Var testrollwidth:=inputwidth;
      If (cont.lines<11)
        testrollwidth-=144;
      EndIf

      If (width+testwidth<testrollwidth)
        cont.data[cont.lines+6+buttons]:=cont.data[cont.lines+6+buttons]+testword;
        width+=testwidth;
        index+=1;
      Else
        width:=0;
      EndIf
    EndIf
  EndWhile

  Var newline;
  For (newline:=1; newline<=newlines; newline:=newline+1);
    cont.lines:=cont.lines+1;
    cont.data[cont.lines+6+buttons]:="";
  EndFor

  cont.+pages:=CInt(cont.lines/roll.lines);
  If (cont.pages*roll.lines<cont.lines)
    cont.pages:=cont.pages+1;
  EndIf

  Return (cont);
EndFunction

// ToDo: Alles was drueber ist, noch optimieren...

////////////////////////////////////////////
// Layout der Schriftrolle zusammenbasteln
////////////////////////////////////////////

Function Create_RollLayout(roll, data, x, y, pages, buttons)
  Var layout:={"page 0", "nodispose"};

  layout.append("resizepic "+CStr(x)+" "+CStr(y)+" "+CStr(roll.gumpbase)+
    " "+CStr(roll.gump_width)+" "+CStr(roll.gump_height));

  Var button_x:=CStr(x+roll.bttn_x)+" ";
  Var text_x:=CStr(x+roll.bttn_x+19)+" ";
  Var refresh_y:=y+roll.bttn_y_refresh;
  Var morelines_y:=y+roll.bttn_y_morelines;

  layout.append("button "+button_x+CStr(refresh_y)+
    " 2117 2118 1 0 "+CStr(bttn_refresh));
  layout.append("text "+text_x+CStr(refresh_y-3)+" 50 1");
  layout.append("button "+button_x+CStr(morelines_y)+
    " 2117 2118 1 0 "+CStr(bttn_morelines));
  layout.append("text "+text_x+CStr(morelines_y-3)+" 50 2");

  Var i;
  For (i:=1; i<=pages; i+=1)
    layout:=Create_RollLayout_Page(roll, data, x, y, pages, i, buttons, layout);
  EndFor

  Return (layout);
EndFunction

Function Create_RollLayout_Page(roll, data, x, y, pages, page, buttons, layout)
  Var gumpbase:=CStr(roll.line_gumpbase)+" ";

  layout.append("page "+CStr(page));

  If (page==1)
    Var x_base:=x+roll.title_x;
    Var y_base:=y+roll.title_y;

    layout.append("gumppic "+CStr(x_base)+" "+CStr(y_base)+" 2204");

    Var x_str1;
    Var x_str2;
    Var y_str;
    Var i_str;
    Var i;

    x_str1:=CStr(x_base+193);
    x_str2:=CStr(x_base+197);
    y_base+=158;
    For (i:=1; i<=3; i+=1)
      y_str:=" "+CStr(y_base+i*20)+" ";
      i_str:=" "+CStr(i+buttons);
      layout.append("resizepic "+x_str1+y_str+gumpbase+"96 20");
      layout.append("textentry "+x_str2+y_str+"88 20 50"+i_str+i_str);
    EndFor
    y_base+=5;
    For (i:=4; i<=5; i+=1)
      y_str:=" "+CStr(y_base+i*20)+" ";
      i_str:=" "+CStr(i+buttons);
      layout.append("resizepic "+x_str1+y_str+gumpbase+"96 20");
      layout.append("textentry "+x_str2+y_str+"88 20 50"+i_str+i_str);
    EndFor

    Var opt1:=Len(data)-buttons;
    Var opt2:=roll.lines+6;
    Var color:=CStr(roll.textcolor);

    Var width1:=CStr(roll.width-132);
    Var width2:=CStr(roll.width-144);
    x_base:=x+roll.line_x;
    x_str1:=CStr(x_base+144);
    x_str2:=CStr(x_base+148);
    y_base:=y+roll.line_y;
    While ((i<opt1) And (i<16))
      y_str:=" "+CStr(y_base+(i-6)*20)+" ";
      i_str:=" "+CStr(i+buttons);
      layout.append("resizepic "+x_str1+y_str+gumpbase+width1+" 20");
      layout.append("textentry "+x_str2+y_str+width2+" 20 "+color+i_str+i_str);
      i+=1;
    EndWhile
    width1:=CStr(roll.width+12);
    width2:=CStr(roll.width);
    x_str1:=CStr(x_base);
    x_str2:=CStr(x_base+4);
    While ((i<opt1) And (i<opt2))
      y_str:=" "+CStr(y_base+(i-6)*20)+" ";
      i_str:=" "+CStr(i+buttons);
      layout.append("resizepic "+x_str1+y_str+gumpbase+width1+" 20");
      layout.append("textentry "+x_str2+y_str+width2+" 20 "+color+i_str+i_str);
      i+=1;
    EndWhile
  Else
    Var x_str1:=CStr(x+roll.line_x);
    Var x_str2:=CStr(x+roll.line_x+4);
    Var y_base:=y+roll.line_y;
    Var y_str;
    Var width1:=CStr(roll.width+12);
    Var width2:=CStr(roll.width);
    Var color:=CStr(roll.textcolor);
    Var i_str;

    Var line_base:=(page-1)*roll.lines+6;
    Var opt1:=Len(data)-buttons;
    Var opt2:=line_base+roll.lines;
    Var i:=line_base;
    While ((i<opt1) And (i<opt2))
      y_str:=" "+CStr(y_base+(i-line_base)*20)+" ";
      i_str:=" "+CStr(i+buttons);
      layout.append("resizepic "+x_str1+y_str+gumpbase+width1+" 20");
      layout.append("textentry "+x_str2+y_str+width2+" 20 "+color+i_str+i_str);
      i+=1;
    EndWhile
  EndIf

  layout:=Create_ScrollBar(x+roll.line_x+roll.width+16, y+roll.line_y,
    roll.lines*20, pages, page, layout);

  Return (layout);
EndFunction

////////////////////////
// Inhalt zurueckgeben
////////////////////////

Function Get_Content(result, lines, buttons)
  Var cont:=content;
  Var opt:=lines+6;
  Var elem;
  Var i;

  Var titleauthor:={""};
  For (i:=1; i<=5; i:=i+1)
    elem:=result[i+buttons];
    elem[1, Find(elem, ": ", 1)+1]:="";
    titleauthor.append(Elem);
  EndFor
  cont.titleauthor:=titleauthor;

  Var story:=result[6+buttons];
  story[1, Find(story, ": ", 1)+1]:="";
  For (i:=7; i<opt; i:=i+1)
    elem:=result[i+buttons];
    elem[1, Find(elem, ": ", 1)+1]:="";
    If (elem<>"")
      story:=story+" "+elem;
    EndIf
  EndFor
  cont.story:=story;

 // SysLog("!!! FICKEN - 1 !!! = ["+cont+"]");

  Return (cont);
EndFunction
