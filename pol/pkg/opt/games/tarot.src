//////////////////////////////////////////////////////////
// Fortune telling script which picks a random fortune
// used in conjunction with tarot cards.
//
// GM Basara - April 22 2001

use os;
use uo;
use util;
include "common";
include "include/msgs";

Program Tarot(who, item)
  If (GetObjProperty(item, "inuse") == 1)
    SendSysMessagePergon(who,
      "Die Geister können nur eine Zukunft auf einmal sehen!",
      "No spirit can see two futures at once!"
    );
    return;
  EndIf

  var tgt := CheckFortuneTarget(who, item);
  If (!tgt)
    return;
  EndIf

  SetObjProperty(item, "inuse", 1);

  PrintTextAboveLocalizedPergon(who,
    "*"+who.name+" mischt die Karten*",
    "*"+who.name+" shuffles cards*"
  );
  If (item.graphic == 0x12ac);
    var i;
    For (i := 1; i <= 4; i += 1)
      item.graphic := 0x12aa;
      Sleep(1);
      item.graphic := 0x12ac;
      Sleep(1);
    EndFor
  ElseIf (item.graphic == 0x12ab);
    var i;
    For (i := 1; i <= 4; i += 1)
      item.graphic := 0x12a9;
      Sleep(1);
      item.graphic := 0x12ab;
      Sleep(1);
    EndFor
  EndIf

  Sleep(2);
  PrintTextAboveLocalizedPergon(who,
    "*"+who.name+" legt die Karten und beginnt, "+
    tgt.name+"s Zukunft zu lesen*",
    "*"+who.name+" reveals the cards and starts to read "+
    tgt.name+"'s future*"
  );

  If (item.graphic == 0x12ac)
    item.graphic := 0x12a6;
  ElseIf (item.graphic == 0x12ab)
    item.graphic := 0x12a5;
  EndIf
  Sleep(6);

  If (item.graphic == 0x12a6)
    item.graphic := 0x12a7;
  ElseIf (item.graphic == 0x12a5)
    item.graphic := 0x12a8;
  EndIf
  Sleep(6);

  TellFortune(who);

  Sleep(16);
  PrintTextAboveLocalizedPergon(item,
    "*"+who.name+" legt die Karten zurück auf den Stapel.",
    "*"+who.name+" puts the cards back on the deck*"
  );

  If (item.graphic == 0x12a7)
    item.graphic := 0x12a6;
  ElseIf (item.graphic == 0x12a8)
    item.graphic := 0x12a5;
  EndIf
  Sleep(3);

  If (item.graphic == 0x12a6)
    item.graphic := 0x12aa;
  ElseIf (item.graphic == 0x12a5)
    item.graphic := 0x12a9;
  EndIf
  Sleep(3);

  If (item.graphic == 0x12aa)
    item.graphic := 0x12ac;
  ElseIf (item.graphic == 0x12a9)
    item.graphic := 0x12ab;
  EndIf

  Detach();
  Sleep(60);
  EraseObjProperty(item, "inuse");
EndProgram
