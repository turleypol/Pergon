//
//  Autor:    Hotny & Turley
//  Date:     28.10.2007
//
//  Controllskript für Feld Nr. 29

//      Aufgaben:
//                - Initialisierung
//                - Feldkontrolle
//                  - Solange die Scherken leben ist Boss invul
//
//  Modifikation:
//

Use uo;
Use os;
Use cfgfile;
Use util;

Include ":dyndungeon:dyndungeon";  // Konstanten
Include "include/starteqp";
Include "include/eventid";
Include "include/eventwaiter";
Include "include/modifyskill";

CONST FIELD_NR:=29;
CONST SCHERKENTIMEOUT:=30;

Program FieldControl(param)
  Var Difficulty:=param[1];
  Var Manager:=param[2];
  Var Restart:=param[3];
  param:=0;
  var Boss;
  var Scherken:={};

  //Als erstes mal kurz warten
  SleepMS(250);
  Var oDungeonConfig:=ReadConfigFile(":dyndungeon:dungeon");
  Var oFieldConfig  :=FindConfigElem(oDungeonConfig,FIELD_NR);

  var DungeonX1:=CInt(SplitWords(oFieldConfig.Corner1)[1]);
  var DungeonY1:=CInt(SplitWords(oFieldConfig.Corner1)[2]);
  var DungeonX2:=CInt(SplitWords(oFieldConfig.Corner2)[1]);
  var DungeonY2:=CInt(SplitWords(oFieldConfig.Corner2)[2]);

  //Spawnen wir zu ersteinmal den Boss Persönlich
  var SpawnBoss:=GetConfigString(oFieldConfig,"SpawnBoss");
  If (!Restart)
    Boss:=Spawn(SpawnBoss,Difficulty);
    Boss.enable("invul");
    IncRevision(Boss);
    Boss.setprop(CPMOB_BOSS,1); // als Boss markieren
  Else
    Boss:=FindBossAfterRestart(DungeonX1,DungeonY1,DungeonX2,DungeonY2);
  EndIf
  If (Boss)
    Boss.setprop(CPMOB_CONTROL,GetPID());
  EndIf

  var SpawnScherken:=GetConfigStringArray(oFieldConfig,"SpawnNPC");
  If (!Restart)
    ForEach Scherke In SpawnScherken
      var npc_:=Spawn(Scherke,Difficulty);
      If (npc_)
        npc_.setprop(CPMOB_CONTROL,GetPID());
        Scherken.Append(npc_);
      EndIf
      SleepMS(2);
    EndForEach
  Else
    FindMobsAfterRestart(DungeonX1,DungeonY1,DungeonX2,DungeonY2, Scherken);
  EndIf

  If (!Restart) //Special's gibts nur einmal
    var SpawnSpecials:=GetConfigStringArray(oFieldConfig,"SpawnSpecial");
    ForEach Special in SpawnSpecials
      SpawnSpecial(Special,Difficulty);
      SleepMS(2);
    EndForEach
  EndIf

  var event;
  var Aktiv:=0;
  var player:={};
  var temp:={};
  While(Boss)
    //So lange der Boss noch lebt

    event:=Wait_For_Event(9999);
    If (event)
      //Ich habe ein Event bekommen -> schauen wir mal was es ist
      Case (event.type)
        EVID_NPC_DIED:
          If (!Boss)
            //mal so aus Sicherheit
            Return;
          EndIf
          NpcDeath(Boss,Scherken);
          
        EVID_DUNGEONTIMECONTROL:
          TimeControl(Boss,Scherken,SpawnScherken,Difficulty);
          
        EVID_DUNGEONBOSSFIELDAKTIV:
          Aktiv:=1;
      EndCase
    EndIf
    
    // Manager nach Anzahl der Player fragen
    Manager.sendevent(struct{type:=EVID_DUNGEONASKFORPLAYER,
                             source:=GetProcess(GetPID()),
                             fieldid:=FIELD_NR});
    While (1)
      event:=Wait_For_Event(2);
      If (event)
        If (event.type==EVID_DUNGEONASKFORPLAYER)
          player:=event.players;
          Break;
        Else  // falls blöderweise was dazwischenkommt
          Case (event.type)
            EVID_NPC_DIED:
              If (!Boss)
                //mal so aus Sicherheit
                Return;
              EndIf
              NpcDeath(Boss,Scherken);
          
            EVID_DUNGEONTIMECONTROL:
              TimeControl(Boss,Scherken,SpawnScherken,Difficulty);
          
            EVID_DUNGEONBOSSFIELDAKTIV:
              Aktiv:=1;
          EndCase
        EndIf
      EndIf
    EndWhile

    //Jetzt schauen wir mal auf Invul-Targets der Targets unserer Kämpfer
    If (player.size())
      If (boss.opponent.enabled("Invul"))
        ForceOpponentChange(boss, SystemFindObjectBySerial(player.randomentry()));
      EndIf
      ForEach scherke in Scherken
        If (scherke.opponent.enabled("Invul"))
          ForceOpponentChange(scherke, SystemFindObjectBySerial(player.randomentry()));
        EndIf
        SleepMS(1);
      EndForEach
    EndIf

    If (Aktiv)
      //Der Dungeon ist noch aktiv -> schauen ob alle da sind, ansonsten Timeout zurücksetzen
      If (player=={})
        //keine Player mehr da -> alles zurücksetzen
        //Boss -> alle Werte hochsetzen
        SetHPPergon(Boss,GetMaxHPPergon(Boss));
        SetManaPergon(Boss,GetMaxManaPergon(Boss));
        SetStaminaPergon(Boss,GetMaxStaminaPergon(Boss));
        temp:=Boss.getprop(CPMOB_COORDS);
        MoveObjectToLocation(Boss,temp[1],temp[2],temp[3],REALM_MALAS);
        //Jeden Scherken auf MaxWerte zurücksetzen
        ForEach Scherke in Scherken
          SetHPPergon(Scherke,GetMaxHPPergon(Scherke));
          SetManaPergon(Scherke,GetMaxManaPergon(Scherke));
          SetStaminaPergon(Scherke,GetMaxStaminaPergon(Scherke));
          temp:=Scherke.getprop(CPMOB_COORDS);
          MoveObjectToLocation(Scherke,temp[1],temp[2],temp[3],REALM_MALAS);
          SleepMS(2);
        EndForEach
        //Jetzt die fehlenden Scherken nachspawnen
        While (Scherken.size() < SpawnScherken.size())
          var npc_:=Spawn(SpawnScherken.randomentry(),Difficulty);
          If (npc_)
            npc_.setprop(CPMOB_CONTROL,GetPID());
            Scherken.Append(npc_);
          EndIf
          SleepMS(2);
        EndWhile
        Aktiv:=0;
      EndIf
    EndIf
  EndWhile
EndProgram

Function NpcDeath(byref Boss, byref Scherken)
  Var i;
  For (i:=1;i<=Scherken.size();i+=1)
    If (!Scherken[i])
      //Den Typen gibt es nicht mehr
      Scherken.erase(i);
      i-=1;
    EndIf
  EndFor
  //Boss InvulFlag
  If (Scherken=={})
    //Es gibt keine Scherken mehr -> Boss wird verwundbar
    If (Boss.enabled("invul"))
      Boss.disable("invul");
      IncRevision(Boss);
    EndIf
    SendEventWaiter(GetProcess(GetPid()), SCHERKENTIMEOUT, EVID_DUNGEONTIMECONTROL);
  Else
    //Es gibt noch/wieder Scherken
    If (!Boss.enabled("invul"))
      Boss.enable("invul");
      IncRevision(Boss);
    EndIf
  EndIf
EndFunction

Function TimeControl(byref Boss, byref Scherken, byref SpawnScherken, Difficulty)
  //Boss beschwört einen neuen Scherken
  If (Scherken=={})
    var npc_:=Spawn(SpawnScherken.randomentry(),Difficulty,Boss.x,Boss.y,Boss.z,0);
    If (npc_)
      npc_.setprop(CPMOB_CONTROL,GetPID());
      Scherken.Append(npc_);
    EndIf
  EndIf
  //Boss InvulFlag
  If (Scherken=={})
    //Es gibt keine Scherken mehr -> Boss wird verwundbar
    If (Boss.enabled("invul"))
      Boss.disable("invul");
      IncRevision(Boss);
    EndIf
  Else
    //Es gibt noch/wieder Scherken
    If (!Boss.enabled("invul"))
      Boss.enable("invul");
      IncRevision(Boss);
    EndIf
  EndIf
EndFunction