// Fackelkontrollstein -- Registriert Fackeln und schickt Sounds

// $Log: not supported by cvs2svn $
// Revision 1.6  2008/07/16 22:30:19  mehdorn
// - Geraeusche seltener und wieder dezenter
//
// Revision 1.5  2008/06/30 14:54:14  mehdorn
// Fackelstein wechselt nun zwischen Feuerfarben und spuckt ab und an Feuer
//
// Revision 1.4  2008/06/29 13:24:03  mehdorn
// Debugging abgeschaltet
//
// Revision 1.3  2008/06/29 13:23:36  mehdorn
// auf Eventsystem umgestellt
//
// Revision 1.2  2008/06/25 10:28:12  mehdorn
// funktioniert nun halbwegs
//
// Revision 1.1  2008/05/23 13:46:47  mehdorn
// verbesserter Fackelansatz
//

use math;
use os;
use uo;
use util;

include "include/eventid";
include "torchcommons";

Const DEBUG    := 0;
Const INTERVAL := 60*5;

Program TorchControl(stone)
    // eigene PID bekanntmachen
    SetGlobalProperty(TORCHCTL_PID_PROP, GetPid());
    Set_Event_Queue_Size(100);

    var burning   := array{
        0xa07, 0xa08, 0xa09, 0xa0c, 0xa0d, 0xa0e, 0xa12, 0xa13, 0xa14
    };
    var col_avail := array{1161, 1258, 1166, 1260, 1174, 1259};
    var col_curr  := 0;
    var torches   := array{};

    var lastsound := 0;
    While (stone)
        Sleep(5);

        If (ReadGameClock() > lastsound + INTERVAL)
            GenerateSounds(torches, burning);
            lastsound := ReadGameClock();
        EndIf

        // ab und an Farbe wechseln und Feuer speien
        col_curr += 1;
        If (col_curr >= col_avail.size())
            col_curr := 0;
            If (RandomInt(10) == 0)
                PlaySoundEffect(stone, 0x478);
                PlayStationaryEffect(
                    stone.x, stone.y, stone.z+17,
                    0x3715, 1, 10, 0, stone.realm
                );
            EndIf
        EndIf
        stone.color := col_avail[col_curr+1];

        RegisterNewTorches(torches);
    EndWhile
EndProgram

Function RegisterNewTorches(byref torches) // {{{
    // ggf. neue Fackeln aufnehmen
    While (Events_Waiting())
        SleepMS(2);
        var ev := Wait_For_Event(0);
        If (!ev)
            continue;
        EndIf
        If (ev.type != EVID_MASTER_HELLO)
            continue;
        EndIf

        var torch := SystemFindObjectBySerial(
            ev.serial,
            SYSFIND_SEARCH_OFFLINE_MOBILES
        );
        torches.append(torch);
        If (DEBUG)
            syslog(
                "Fackel ["+Hex(torch.serial)+"] gefunden bei "+
                torch.x+", "+torch.y+"; "+torches.size()+
                " Fackeln bekannt"
            );
        EndIf
    EndWhile
EndFunction // }}}

Function GenerateSounds(byref torches, byref burning) // {{{
    // Soundeffekte an allen Fackeln generieren
    ForEach torch in (torches)
        SleepMS(2);
        If (!torch)
            // Fackel existiert nicht mehr
            torches.erase(_torch_iter);
            continue;
        ElseIf (torch.container)
            // Fackeln in Containern ignorieren
            continue;
        ElseIf (torch.graphic in burning)
            // Soundlaenge 4,7s - leider sehr leise
            PlaySoundEffect(torch, 0x4bb);
        EndIf
    EndForEach
EndFunction // }}}
