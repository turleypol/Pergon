//**************************************************************************************************
// Script file resistpoison.src
//
//    implements spell 316 <Resist Poison> - "Giftresistenz"
//
//	raises spelltargets resistance against poison
//		 [ Resisted (Magic) ]
//
//	Vers. 0.9	- 19.11.2001 Sebastian 'Bihinos' Giese
//	Vers. 1.1	- 04.10.2003 Sebastian 'Bihinos' Giese - ueberarbeitet
//
//**************************************************************************************************


Include "include/spellcheck";
Include "../magicpergon";

program cast_resistpoison (params)

   Var caster;
   Var caston;
   Var poisonres;

   Var spells:= ReadConfigFile("::spells"); 									// load spelldata
   Var spell:=  FindConfigElem(spells, SPELLID_NECRO_RESISTPOISON);
   Var difficulty:= spell.skill;
   Var resist:=  spell.resist;
   Var resp:=    spell.primary;
   Var minresp:= spell.primarymin;
   Var maxresp:= spell.primarymax;
   Var dur:=     spell.duration;
   Var mindur:=  spell.durationmin;
   Var maxdur:=  spell.durationmax;
   Var power;

   if (params[1] == "#MOB")
      caster:= params[2];
      power:=  params[3];
      caston:= params[4];
   elseif (params[1] == "#SCROLL")
      caster:= params[2];
      power:=  params[3];
      caston:= TargetSpell(caster, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   else
      caster:= params[1];

      power:= GetCastPowerPergon(caster, SPELLID_NECRO_RESISTPOISON);
      caston:= TargetSpell(caster, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   endif

   TurnCharTo(caster, caston);
   PerformAction(caster, ANIM_CAST_AREA);

   PlaySoundEffect(caston, SFX_1EA);
   PlayObjectCenteredEffect(caston, FX_SPARK_EFFECT, 10, 10);

   if (GetObjProperty(caston, "spell_resist"))		// check if spelltarget already has elemental resistance
																	// (only one enchantment at a time)
      Var restypeger;
      Var restypeeng;
      case (GetObjProperty(caston, "spell_resist")[2])
         "poison":	restypeger:= "Giftangriffe";
         				restypeeng:= "Poison Attacs";
         default:		restypeger:= "magische Angriffe";
         				restypeeng:= "Magical Attacs";
      endcase

      SendSysMessagePergon(caster, "Euer Ziel ist bereits gegen " + restypeger + " geschützt",
                                   "Your target is already protected against " + restypeeng);
      return;
   endif

   if (resist)					// calc spelleffect according to spelltargets magic resistance and casting power
      resp:= CInt(Magicresistance(caston, difficulty, resist, power*resp/100.0, 1));
      dur:=  CInt(Magicresistance(caston, difficulty, resist, power*dur/100.0, 0));
   else
  	 	resp:= CInt(power*resp/100.0);
   	dur:=  CInt(power*dur/100.0);
   endif

   if (minresp and minresp > resp)
      resp:= minresp;
   elseif (maxresp and maxresp < resp)
      resp:= maxresp;
   endif
   if (mindur and mindur > dur)
      dur:= mindur;
   elseif (maxdur and maxdur < dur)
      dur:= maxdur;
   endif

   Set_Critical(1);
   poisonres:= CInt(GetObjProperty(caston, "resist_poison"));			// get poison resistance
   SetObjProperty(caston, "resist_poison", poisonres + resp);			// set new resistance
   Set_Critical(0);
   SetCastPowerProperty(caston,"Magie",CInt(resp/2),"","Poison",0);
   RefreshSpellPowerGump(caston);

   var tag:= array;

   tag[1]:= ReadGameClock() + dur;
   tag[2]:= "poison";
   tag[3]:= resp;
   tag[6]:= ReadMilliSecondClock();

   SetObjProperty(caston, "spell_resist", tag);								// tag spelltarget for spellchecker
   SendSpellWaiter(caston, "spell_resist", dur, tag[6], caston);

   if (!caston.isa(POLCLASS_NPC))													// spell has been casted on Player
      PrintTextAbovePrivatePergon(caston, "Ihr spürt, wie Ihr unempfindlicher gegen Gift werdet",
                                          "You feel yourself becoming less vulnerable to Poison", caston);
      SendStatus(caston);	 															//Update Statuswindow
   endif
   
   

endprogram
