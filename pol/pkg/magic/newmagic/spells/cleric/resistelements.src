//**************************************************************************************************
// Script file resistelements.src
//
//    implements spell 222 <Resist Elements> - "Elementarresistenz"
//
//	raises spelltargets resistance against all elements (fire, water, air, earth)
//		 [ Resisted (Magic) ]
//
//	Vers. 0.9	- 19.11.2001 Sebastian 'Bihinos' Giese
//	Vers. 1.1	- 03.10.2003 Sebastian 'Bihinos' Giese - ueberarbeitet
//
//**************************************************************************************************


Include "include/spellcheck";
Include "../magicpergon";

program cast_resistelements (params)

   Var caster;
   Var caston;
   Var oldres:= {};

   Var spells:= ReadConfigFile("::spells"); 									// load spelldata
   Var spell:=  FindConfigElem(spells, SPELLID_CLERIC_RESISTELEMENTS);
   Var difficulty:= spell.skill;
   Var resist:= spell.resist;
   Var res:=    spell.primary;
   Var minres:= spell.primarymin;
   Var maxres:= spell.primarymax;
   Var dur:=    spell.duration;
   Var mindur:= spell.durationmin;
   Var maxdur:= spell.durationmax;
   Var power;

   if (params[1] == "#MOB")
      caster:= params[2];
      power:=  params[3];
      caston:= params[4];
   elseif (params[1] == "#SCROLL")
      caster:= params[2];
      power:=  params[3];
      caston:= TargetSpell(caster, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   else
      caster:= params[1];

      power:= GetCastPowerPergon(caster, SPELLID_CLERIC_RESISTELEMENTS);
      caston:= TargetSpell(caster, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   endif

   TurnCharTo(caster, caston);
   PerformAction(caster, ANIM_CAST_AREA);

   PlaySoundEffect(caston, SFX_215);
   PlayObjectCenteredEffect(caston, FX_SPARK_EFFECT, 15, 15);

   if (GetObjProperty(caston, "spell_resist"))		// check if spelltarget already has elemental resistance
      Var restypeger;										// (only one enchantment at a time)
      Var restypeeng;
      case (GetObjProperty(caston, "spell_resist")[2])
         "elements":	restypeger:= "Elementare Angriffe";
         				restypeeng:= "Elemental Attacs";
         default:		restypeger:= "magische Angriffe";
         				restypeeng:= "Magical Attacs";
      endcase

      SendSysMessagePergon(caster, "Euer Ziel ist bereits gegen " + restypeger + " geschützt",
                                   "Your target is already protected against " + restypeeng);
      return;
   endif
  								// calc spelleffects according to spelltargets magic resistance
   if (resist)				// and casting power
      res:=CInt(Magicresistance(caston, difficulty, resist, power*res/100.0, 1));
      dur:=CInt(Magicresistance(caston, difficulty, resist, power*dur/100.0, 0));
   else
   	res:=CInt(power*res/100.0);
   	dur:=CInt(power*dur/100.0);
   endif

   if (minres and minres > res)
      res:= minres;
   elseif (maxres and maxres < res)
      res:= maxres;
   endif
   if (mindur and mindur > dur)
      dur:= mindur;
   elseif (maxdur and maxdur < dur)
      dur:= maxdur;
   endif

   Set_Critical(1);
   oldres[1]:= CInt(GetObjProperty(caston, "resist_fire"));		// get elemental resistances of spelltarget
   oldres[2]:= CInt(GetObjProperty(caston, "resist_water"));
   oldres[3]:= CInt(GetObjProperty(caston, "resist_air"));
   oldres[4]:= CInt(GetObjProperty(caston, "resist_earth"));

   SetObjProperty(caston, "resist_fire", oldres[1] + res);		// set new elemental resistances
   SetObjProperty(caston, "resist_water", oldres[2] + res);
   SetObjProperty(caston, "resist_air", oldres[3] + res);
   SetObjProperty(caston, "resist_earth", oldres[4] + res);
   Set_Critical(0);
   
   SetCastPowerProperty(caston,"Magie",CInt(res/3),"","Fire",0);
   SetCastPowerProperty(caston,"Magie",CInt(res/3),"","Water",0);
   SetCastPowerProperty(caston,"Magie",CInt(res/3),"","Air",0);
   SetCastPowerProperty(caston,"Magie",CInt(res/3),"","Earth",0);
   RefreshSpellPowerGump(caston);

   var tag:= array;

   tag[1]:= ReadGameClock() + dur;
   tag[2]:= "elements";
   tag[3]:= res;
   tag[4]:= "resist";
   tag[6]:= ReadMilliSecondClock();

   SetObjProperty(caston, "spell_resist", tag);						// tag spelltarget for spellchecker
   SendSpellWaiter(caston, "spell_resist", dur, tag[6], caston);

   if (!caston.isa(POLCLASS_NPC))											// spell has been casted on Player
      PrintTextAbovePrivatePergon(caston, "Ihr spürt, wie Ihr unempfindlicher gegen alle Elemente werdet",
                                          "You feel yourself becoming less vulnerable to all elements", caston);
      SendStatus(caston);	 															//Update Statuswindow
   endif   

endprogram
