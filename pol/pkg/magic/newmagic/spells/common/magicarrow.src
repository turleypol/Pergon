//**************************************************************************************************
// Script file magicarrow.src
//
//    implements spell 6 <Magic Arrow> - "Magischer Pfeil"
//
//	deals very minor magic damage to spelltarget
//		 [ Reflected / Resisted (Magic) ]
//
//	Vers. 0.9	- 19.11.2001 Sebastian 'Bihinos' Giese
//	Vers. 1.1	- 27.06.2003 Sebsatian 'Bihinos' Giese - ueberarbeitet
//
//**************************************************************************************************
use vitals;

Include "../magicpergon";
Include "../resistance";
Include "../spellattack";
Include "include/packets";

program cast_magicarrow (params)

   Var caster;
   Var caston;
   Var spelltarget;

   Var spells:= ReadConfigFile("::spells"); 									// load spelldata
   Var spell:=  FindConfigElem(spells, SPELLID_COMMON_MAGICARROW);
   Var difficulty:= spell.skill;
   Var resist:= spell.resist;
   Var dmg:=    spell.primary;
   Var mindmg:= spell.primarymin;
   Var maxdmg:= spell.primarymax;
   Var power;

   if (params[1] == "#MOB")
      caster:= params[2];
      power:=  params[3];
      caston:= params[4];
      send_attack(caston, caster, 6);
   elseif (params[1] == "#SCROLL")
      caster:= params[2];
      power:=  params[3];
      caston:= TargetSpell(caster, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   else
      caster:= params[1];

      power:= GetCastPowerPergon(caster, SPELLID_COMMON_MAGICARROW);
      caston:= TargetSpell(caster, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   endif

   TurnCharTo(caster, caston);
   PerformAction(caster, ANIM_CAST_DIR);
					 												// get spelltarget -> possible spell reflection
   spelltarget:= ReflectSpell(caster, caston, dmg, 0, FX_MAGIC_ARROW, 5, 1, 0, SFX_SPELL_MAGIC_ARROW);

   if (spelltarget)				// check and alter damage according to spelltargets magic resistance
      if (resist)					// and casting power
        dmg:= CInt(MagicResistance(spelltarget, difficulty, resist, power*dmg/100.0, 1));
      else
      	dmg:= CInt(power*dmg/100.0);
      endif

      if (mindmg and mindmg > dmg)
         dmg:= mindmg;
      elseif (maxdmg and maxdmg < dmg)
         dmg:= maxdmg;
      endif
      ApplyRawDamagePergon(spelltarget, dmg);				// deal damage
   endif

endprogram
