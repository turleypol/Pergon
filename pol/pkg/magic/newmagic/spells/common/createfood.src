//**************************************************************************************************
// Script file createfood.src
//
//    implements spell 1 <Create Food> - "Essen"
//
//	creates something to eat in casters backpack
//
//	Vers. 0.9	- 19.11.2001 Sebastian 'Bihinos' Giese
//	Vers. 1.1	- 27.06.2003 Sebsatian 'Bihinos' Giese - ueberarbeitet
//
//**************************************************************************************************

use util;

Include "../magicpergon";

program cast_createfood (params)

   Var caster;
   Var food;
   Var effect;
   Var power;

   if (params[1] == "#SCROLL")
      caster:= params[2];
      power:=  params[3];
   else
      caster:= params[1];

      power:= GetCastPowerPergon(caster, SPELLID_COMMON_CREATEFOOD);
   endif

   PerformAction(caster, ANIM_CAST_AREA);

   effect:= CInt(power/5);									// calc spelleffect depending on casting power
   if (effect > 21)
      effect:= 21;
   endif

   Var level:= RandomInt(effect);						// choose food to be created

   case (level)
      0:		food:= 0x09D0;			// apple
      1:		food:= 0x0994;			// pear
      2:		food:= 0x172C;			// peach
      3:		food:= 0x1720;			// banana
      4:		food:= 0x097D;			// cheese
      5:		food:= 0x1044;			// spam
      6:		food:= 0x097E;			// cheese wheel
      7:		food:= 0x103B;			// bread loaf
      8:		food:= 0x1608;			// chicken leg
      9:		food:= 0x160C;			// cookies
      10:	food:= 0x09FA;			// muffins (some)
      11:	food:= 0x09EB;			// muffins (some more)
      12:	food:= 0x098C;			// baguette
      13:	food:= 0x097B;			// fish filet
      14:	food:= 0x1040;			// pizza
      15:	food:= 0x1041;			// pie
      16:	food:= 0x09E9;			// cake
      17:	food:= 0x09C9;			// ham
      18:	food:= 0x0977;			// bacon slab
      19:	food:= 0x09B8;			// chicken
      20:	food:= 0x09F2;			// ribs
   endcase

   PlaySoundEffect(caster, SFX_SPELL_CREATE_FOOD);
   CreateItemInBackpackPergon(caster, food, 1);		// create food

endprogram
