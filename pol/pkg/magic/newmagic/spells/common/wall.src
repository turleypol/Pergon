//**************************************************************************************************
// Script file wall.src
//
//    implements spell 27 <Wall> - "Mauer"
//
//	magically raises wall
//
//	Vers. 0.9	- 19.11.2001 Sebastian 'Bihinos' Giese
//	Vers. 1.1	- 27.06.2003 Sebastian 'Bihinos' Giese - ueberarbeitet
//
//**************************************************************************************************

use os;
use util;

Include "../magicpergon";
Include "../fields";

program cast_wall (params)

   Var caster;
   Var where;

   Var spells:= ReadConfigFile("::spells"); 									// load spelldata
   Var spell:=  FindConfigElem(spells, SPELLID_COMMON_WALL);
   Var dur:=    spell.duration;
   Var mindur:= spell.durationmin;
   Var maxdur:= spell.durationmax;
   Var power;

   if (params[1] == "#MOB")
      caster:= params[2];
      power:=  params[3];
      where:=struct{x,y,z,realm};
      where.x:=params[4].x;
      where.y:=params[4].y;
      where.z:=params[4].z;
      where.realm:=params[4].realm;
   elseif (params[1] == "#SCROLL")
      caster:= params[2];
      power:=  params[3];
      where:=  TargetArea(caster);
      if (!where)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   else
      caster:= params[1];

      power:= GetCastPowerPergon(caster, SPELLID_COMMON_WALL);
      where:= TargetArea(caster);
      if (!where)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   endif

   TurnCharTo(caster, where);
   PerformAction(caster, ANIM_CAST_AREA);

   Var fields:= {};

   Var x:= where.x;
   Var y:= where.y;
   Var z:= where.z;
														// calc direction of wall using function from fields.inc
   case (GetDirection(caster, x, y))		// create wall
      "nord":	fields:= CreateNorthSouthField(caster, power, UOBJ_STONEWALL_NS, x, y, z, 2);
      "sued":	fields:= CreateNorthSouthField(caster, power, UOBJ_STONEWALL_NS, x, y, z, 2);
      "ost":	fields:= CreateEastWestField(caster, power, UOBJ_STONEWALL_EW, x, y, z, 2);
      "west":	fields:= CreateEastWestField(caster, power, UOBJ_STONEWALL_EW, x, y, z, 2);
      default:	case (randomint(2))
		   			      0:	fields:= CreateNorthSouthField(caster, power, UOBJ_STONEWALL_NS, x, y, z, 2);
        		      1:	fields:= CreateEastWestField(caster, power, UOBJ_STONEWALL_EW, x, y, z, 2);
					      endcase
   endcase

   Var i;
   for (i:= 1; i <= 5; i+= 1)
      PlaySoundEffect(fields[i], SFX_SPELL_WALL_OF_STONE);
   endfor

   detach();

   dur:= CInt(power*dur/100.0);				// calc duration of spell according to casting power
   if (mindur and mindur > dur)
      dur:= mindur;
   elseif (maxdur and maxdur < dur)
      dur:= maxdur;
   endif

   sleep(dur);										// and wait

   foreach fieldpart in fields				// destroy wall
      DestroyItem(fieldpart);
   endforeach

endprogram
