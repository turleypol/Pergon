//**************************************************************************************************
// Script file frostshock.src
//
//    implements spell 110 <Frost Shock> - "Frostschock"
//
//	fires frostshock on spelltarget - deals more water(ice) damage
//		 [ Reflected / Resisted (Elemental - Water) ]
//
//	Vers. 0.9	- 19.11.2001 Sebastian 'Bihinos' Giese
//	Vers. 1.1	- 30.06.2003 Sebastian 'Bihinos' Giese - ueberarbeitet
//
//**************************************************************************************************
use vitals;
include "../magicpergon";
include "../resistance";
include "../spellattack";
include "include/packets";

program cast_frostshock (params)

   Var caster;
   Var caston;
   Var spelltarget;

   Var spells:= ReadConfigFile("::spells"); 									// load spelldata
   Var spell:=  FindConfigElem(spells, SPELLID_MAGE_FROSTSHOCK);
   Var resist;
   Var dmg:=    spell.primary;
   Var mindmg:= spell.primarymin;
   Var maxdmg:= spell.primarymax;
   Var power;

   if (params[1] == "#MOB")
      caster:= params[2];
      power:=  params[3];
      caston:= params[4];
      send_attack(caston, caster, 110);
   elseif (params[1] == "#SCROLL")
      caster:= params[2];
      power:=  params[3];
      caston:= TargetSpell(caster, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   else
      caster:= params[1];
      
      power:= GetCastPowerPergon(caster, SPELLID_MAGE_FROSTSHOCK);
      caston:= TargetSpell(caster, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   endif

   TurnCharTo(caster, caston);
   PerformAction(caster, ANIM_CAST_DIR);
																	// get spelltarget -> possible spell reflection
   spelltarget:= ReflectSpell(caster, caston, dmg, 1, FX_SMOKE, 10, 10, 0, SFX_1D4);

   if (spelltarget)
      resist:= 1 - CDbl(GetResistance(spelltarget, "water"));	// get water resistance of spelltarget
      dmg:= CInt(power*resist*dmg/100.0);								// calc and alter spell damage
      if (mindmg and mindmg > dmg)
         dmg:= mindmg;
      elseif (maxdmg and maxdmg < dmg)
         dmg:= maxdmg;
      endif
      ApplyRawDamagePergon(spelltarget, dmg);								// apply damage to spelltarget
   endif

endprogram
