//**************************************************************************************************
// Script file resistearth.src
//
//    implements spell 118 <Resist Earth> - "Erdresistenz"
//
//	raises spelltargets resistance against earth
//	slightly lowers spelltargets resistance against air
//		 [ Resisted (Magic) ]
//
//	Vers. 0.9	- 19.11.2001 Sebastian 'Bihinos' Giese
//	Vers. 1.1	- 28.06.2003 Sebastian 'Bihinos' Giese - ueberarbeitet
//
//**************************************************************************************************


Include "include/spellcheck";
Include "../magicpergon";

program cast_resistearth (params)

   Var caster;
   Var caston;
   Var earthres;
   Var airres;

   Var spells:= ReadConfigFile("::spells"); 									// load spelldata
   Var spell:=  FindConfigElem(spells, SPELLID_MAGE_RESISTEARTH);
   Var difficulty:= spell.skill;
   Var resist:=  spell.resist;
   Var rese:=    spell.primary;
   Var minrese:= spell.primarymin;
   Var maxrese:= spell.primarymax;
   Var resa;
   Var dur:=     spell.duration;
   Var mindur:=  spell.durationmin;
   Var maxdur:=  spell.durationmax;
   Var power;

   if (params[1] == "#MOB")
      caster:= params[2];
      power:=  params[3];
      caston:= params[4];
   elseif (params[1] == "#SCROLL")
      caster:= params[2];
      power:=  params[3];
      caston:= TargetSpell(caster, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   else
      caster:= params[1];

      power:= GetCastPowerPergon(caster, SPELLID_MAGE_RESISTEARTH);
      caston:= TargetSpell(caster, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS);
      if (!caston)
         SendSysMessagePergon(caster, "Kein Ziel", "No target");
         return;
      endif
   endif

   TurnCharTo(caster, caston);
   PerformAction(caster, ANIM_CAST_AREA);

   PlaySoundEffect(caston, SFX_1EA);
   PlayObjectCenteredEffect(caston, FX_SPARK_EFFECT, 10, 10);

   if (GetObjProperty(caston, "spell_resist"))		// check if spelltarget already has elemental resistance
																	// (only one enchantment at a time)
      Var restypeger;
      Var restypeeng;
      case (GetObjProperty(caston, "spell_resist")[2])
         "fire":	restypeger:= "Feuerangriffe";
         			restypeeng:= "Fire Attacs";
         "water":	restypeger:= "Wasserangriffe";
         			restypeeng:= "Water Attacs";
         "air":	restypeger:= "Luftangriffe";
         			restypeeng:= "Air Attacs";
         "earth":	restypeger:= "Erdangriffe";
         			restypeeng:= "Earth Attacs";
         default:	restypeger:= "magische Angriffe";
         			restypeeng:= "Magical Attacs";
      endcase

      SendSysMessagePergon(caster, "Euer Ziel ist bereits gegen " + restypeger + " geschützt",
                                   "Your target is already protected against " + restypeeng);
      return;
   endif

   if (resist)					// calc spelleffect according to spelltargets magic resistance and casting power
      rese:= CInt(Magicresistance(caston, difficulty, resist, power*rese/100.0, 1));
      dur:=  CInt(Magicresistance(caston, difficulty, resist, power*dur/100.0, 0));
   else
      rese:= CInt(power*rese/100.0);
      dur:=  CInt(power*dur/100.0);
   endif

   if (minrese and minrese > rese)
      rese:= minrese;
   elseif (maxrese and maxrese < rese)
      rese:= maxrese;
   endif
   resa:= CInt(rese / 2);   					// get resulting loss of air resistance
   if (mindur and mindur > dur)
      dur:= mindur;
   elseif (maxdur and maxdur < dur)
      dur:= maxdur;
   endif

   Set_Critical(1);
   earthres:= CInt(GetObjProperty(caston, "resist_earth"));		// get resistances of spelltarget
   airres:=   CInt(GetObjProperty(caston, "resist_air"));
   SetObjProperty(caston, "resist_earth", earthres + rese);		// set new resistances
   SetObjProperty(caston, "resist_air", airres - resa);
   Set_Critical(0);
   SetCastPowerProperty(caston,"Magie",CInt(resa/4),"","Air",1);
   SetCastPowerProperty(caston,"Magie",CInt(rese/2),"","Earth",0);
   RefreshSpellPowerGump(caston);

   var tag:= array;

   tag[1]:= ReadGameClock() + dur;
   tag[2]:= "earth";
   tag[3]:= rese;
   tag[6]:= ReadMilliSecondClock();

   SetObjProperty(caston, "spell_resist", tag);						// tag spelltarget for spellchecker
   SendSpellWaiter(caston, "spell_resist", dur, tag[6], caston);

   if (!caston.isa(POLCLASS_NPC))											// spell has been casted on Player
      PrintTextAbovePrivatePergon(caston, "Ihr spürt, wie Ihr unempfindlicher gegen Stein werdet",
                                          "You feel yourself becoming less vulnerable to Earth", caston);
      SendStatus(caston);	 															//Update Statuswindow
   endif

	 
	 
endprogram
