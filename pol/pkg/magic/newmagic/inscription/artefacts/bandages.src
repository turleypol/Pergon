//////////////////////////////////////////////////////////////////////////
// Doppelklickscript für Bandagen
//
// Bandagen die das Ziel heilen

//////////////////////////////////////////////////////////////////////////
// Modification:
// $Log: not supported by cvs2svn $
// Revision 1.8  2008/08/31 20:29:10  mehdorn
// - leicht ueberarbeitet
// - Tippfehler raus
// - teilweise Uebersetzungen vervollstaendigt/verbessert
// - diverse unnoetige Includes entfernt
//
// 2007-02-15 - Seppel
// Skillabfrage auf Gesegnete Artefakte oder Segnen
//
// 2006-01-21 - Seppel
// Benutzungsdelay auslogsicher gemacht
//
// 2005-12-03 - Seppel
// erstellt

include ":newspells:resistance";
include "common";
include "include/modifyskill";
include "include/packets";
use cfgfile;
use os;
use uo;
use util;

Program Bandages(who, item)
  If (!ArtefactUsable(who, item, ARTEFACT_DELAY))
    return;
  EndIf

  SendSysMessagePergon(who,
    "Wen wollt ihr heilen?", "Who do you want to heal?"
  );

  var usedon := Target(who);
  If (!usedon)
    SendSysMessagePergon(who, "Zielen abgebrochen");
    return;
  ElseIf (Distance(who, usedon) >= 2)
    SendSysMessagePergon(who, "Ihr müsst näher an euer Ziel heran");
    return;
  ElseIf (!CheckLineOfSight(who, usedon) or usedon.hidden)
    SendSysMessagePergon(who, "Ihr könnt Euer Ziel nicht sehen.");
    return;
  ElseIf (!usedon.isa(POLCLASS_MOBILE))
    SendSysMessagePergon(who,
      "Ihr könnt nur Lebendes verarzten",
      "You can only heal living things"
    );
    return;
  EndIf

  var artefacts := ReadConfigFile(":newinscription:artefacts");
  var artefact := FindConfigElem(artefacts, GetArtefactID(item));
  var delay := artefact.delay;
  var difficulty := artefact.useskill;
  // Delay zwischen Benutzung und Heilung
  var healDelay := SplitWords(GetConfigString(artefact, "HealDelay"));
  var minHealDelay := CInt(healDelay[1]);
  var maxHealDelay := CInt(healDelay[3]);
  // Staerke der Heilung
  var heal := SplitWords(GetConfigString(artefact, "Heal"));
  var minHeal := CInt(heal[1]);
  var maxHeal := CInt(heal[3]);

  // die niederen Bandagen kann jeder nutzen
  If (
    difficulty and
    (GetSkillPergon(who, SKILLID_GESEGNETEGEGENST) < difficulty) and
    (GetSkillPergon(who, SKILLID_SEGNEN) < difficulty)
  )
    SendSysMessagePergon(who,
      "Ihr könnt das nicht benutzen", " You cannot use this"
    );
    return;
  EndIf

  // Zeitpunkt setzen ab wann wieder Artefakt nutzbar
  SetObjProperty(who, "#UsesSomething", (ReadGameClock() + delay));

  If (usedon == who)
    SendSysMessagePergon(who, "Ihr beginnt Euch zu verarzten");
  Else
    SendSysMessagePergon(who, "Ihr beginnt "+usedon.name+" zu verarzten");
    SendSysMessagePergon(usedon, who.name+" beginnt Euch zu verarzten");
  EndIf

  var rawHeal := RandomIntMinMax(minHeal, 1+maxHeal);
  Sleep(RandomIntMinMax(minHealDelay, 1+maxHealDelay));

  SubtractAmount(item, 1);
  PlaySoundEffect(usedon, 0x0203);
  HealDamage(usedon, rawHeal);
  Detach();

  If (usedon <> who)
    SendSysMessagePergon(who,
      "Ihr heilt "+usedon.name+" "+rawHeal+" Hitpoints",
      "You heal "+usedon.name+" "+rawHeal+" hitpoints"
    );
    SendSysMessagePergon(who,
      who.name+" heilt Euch "+rawHeal+" Hitpoints",
      who.name+" heals you "+rawHeal +"hitpoints"
    );
  Else
    SendSysMessagepergon(who,
      "Ihr heilt Euch "+rawHeal+" Hitpoints",
      "You heal yourself "+rawHeal+" hitpoints"
    );
  EndIf
EndProgram

// vim: sw=2 sts=2
