//
// 20.02.2001 - Fraggulus - Bugfixing der Namen des Bretts und der Ueberschriften
//

use uo;

Include "include/clock";
Include "include/msgs";

program messageboard( user, board )
  Var curday := ReadGameClock()/(PERGON_LENGTH_DAY*60);
  Var input, i, oldday, topic, space, age;

  Var layout := {
    "page 0",
    "resizepic 50 50 5120 540 380",
    "resizepic 75 25 5120 200 50",
    "resizepic 365 405 5120 200 50",
    "button 395 420 2121 2120 1 0 1",
    "button 475 420 2130 2129 3 2 2",
    "text 100 40 45 0",
    "text 80 390 45 1",

    "radio 100 100 5002 5003 1 1001",
    "radio 100 150 5002 5003 0 1002",
    "radio 100 200 5002 5003 0 1003",
    "radio 100 250 5002 5003 0 1004",
    "radio 100 300 5002 5003 0 1005",
    "radio 100 350 5002 5003 0 1006",

    "text 140  98 45 2",
    "text 140 148 45 3",
    "text 140 198 45 4",
    "text 140 248 45 5",
    "text 140 298 45 6",
    "text 140 348 45 7",

    "radio 330 100 5002 5003 0 1007",
    "radio 330 150 5002 5003 0 1008",
    "radio 330 200 5002 5003 0 1009",
    "radio 330 250 5002 5003 0 1010",
    "radio 330 300 5002 5003 0 1011",
    "radio 330 350 5002 5003 0 1012",

    "text 370  98 45 8",
    "text 370 148 45 9",
    "text 370 198 45 10",
    "text 370 248 45 11",
    "text 370 298 45 12",
    "text 370 348 45 13"

  // "textentry 100 80 150 50 32 1050 2",
  // "checkbox 10 10 210 211 1 2002",
  // "checkbox 10 30 210 211 0 2001"
  };

  Var data := {
    board.desc,
    "Willkommen am Schwarzen Brett!"
  };

  // data[1] := board.name;

  for ( i := 1; i <= 12; i += 1 )
    topic := GetObjProperty( board, "Thema" + cstr(i) );
    if ( topic == error )
      data[i+2] := "[leer]";
    else
      space := find ( topic, " ", 1 );
      oldday := topic[1,space-1];
      age := curday - cint(oldday);
      if (age == 1)
        data[i+2] := topic[space+1, 10] + " (1 Tag)";
      else
        topic := topic[space+1, 10];
        data[i+2] := topic + " (" + age + " Tage)";
      endif
    endif
  endfor

  Var res := SendDialogGump( user, layout, data );
  if (res[0] != 2)  //  Antwort != "OK"
    return;
  endif

  Var selected;
  foreach key in (res.keys)
    if (key > 1000)
      selected := cstr(key - 1000);
      break;
    endif
  endforeach

  print ( "Gewaehlt: " + selected );
  Var text := GetObjProperty( board, "Text" + selected );
  if (text == error)
    SendSysMessagePergon( user, "Gebt eine KURZE Überschrift" );
    topic := RequestInputPergon(user, board, "(10 Zeichen ist ideal)");
    if (!topic)
      SendSysMessagePergon( user, "Abbruch", "Abort");
      return;
    endif
    topic := CChrZ(topic.uc_text);
    text := RequestInputPergon(user, board, "Zeile 1:");
    if (!text)
      SendSysMessagePergon( user, "Abbruch", "Abort");
      return;
    endif
    text := CChrZ(text.uc_text);
    for (i := 2; i <= 17; i += 1 )
      input := RequestInputPergon(user, board, "Zeile " + i + ": (ESC=Abbruch)");
      if (input)
        input := CChrZ(input.uc_text);
        text += "#" + input;
      else
        break;
      endif
    endfor
    SetObjProperty( board, "Thema"+selected, cstr(curday) + " " + topic );
    SetObjProperty( board, "Text"+selected, text );
    SendSysMessagePergon( user, "Abbruch", "Abort");
    return;
  endif

  layout := {
    "page 0",
    "resizepic 50 50 5120 540 380",
    "resizepic 75 25 5120 400 50",
    "resizepic 365 405 5120 200 50",
    "button 395 420 2121 2120 1 0 1",
    "button 475 420 2130 2129 3 2 2",
    "checkbox 80 392 210 211 0 3",
    "text 110 390 45 1",
    "text 100 40 45 0"
  };

  topic := GetObjProperty( board, "Thema" + selected );
  if ( topic == error )
    data[1] := "(Kein Thema)";
  else
    space := find ( topic, " ", 1 );
    data[1] := topic[space+1, len(topic)-space];
  endif
  data[2] := "Loeschen";

  Var crlf;
  Var line := 1;
  repeat
    crlf := find( text, "#", 1 );
    layout[line+9] := "text 100 " + (82 + line * 16) + " 45 " + (line + 1);
    data[line+2] := text[1, crlf-1];
    text := text[crlf+1,len(text)-crlf];
    line := line + 1;
  until (!crlf);

  res := SendDialogGump( user, layout, data );

  if ( !res[3] )
    return;  // don't delete msg
  endif

  oldday := topic[1,space-1];
  age := curday - cint(oldday);
  if (age < 2)
    SendSysMessagePergon( user, "Nachricht ist erst " + age + " Tag(e) alt." );
    SendSysMessagePergon( user, "Ist nicht gelöscht worden." );
  else
    EraseObjProperty( board, "Text" + selected );
    EraseObjProperty( board, "Thema" + selected );
    SendSysMessagePergon( user, "Nachricht " + selected + " gelöscht." );
  endif
endprogram
