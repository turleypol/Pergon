///////////////////////////////////////////////////////////////////////////
// $Id: blutglas.src,v 1.25 2010-07-15 21:47:38 Mentholia Exp $
// 13.06.2002 - Blutglas - Fraggulus
// extrahiert aus einer (Drachen-)Leiche einen Blutflakon bzw. Drachenblut
// pro Leiche ist dies nur 1x moeglich
// ausgenommen sind Skelette und summoned Monster
// Skill: Obduktion

use attributes;
use os;
use uo;
use util;
include "include/client";
include "include/modifyskill";
include "include/objtype";
include "include/set";

// beinhaltet Templates aller Sonderleichen; alle anderen Leichen werden
// als normale gewertet
Var DrachenLeichenTemplates  := {
    0x000c, 0x002e, 0x003b, 0x003c, 0x003d,
    0x003e, 0x006a, 0x00b4, 0x031a, 0x031f
};
Var DaemonenLeichenTemplates := {
    0x0009, 0x0026, 0x002b, 0x0066, 0x0095,
    0x013e, 0x0310, 0x0318, 0x031b
};
Var SkelettLeichenTemplates  := {
    0x0032, 0x0038, 0x0039, 0x0068
};
Var TrollLeichenTemplates    := {
    0x0035, 0x0036, 0x0037
};
Var EinhornLeichenTemplates  := {
    0x007A
};

Var KeinBlutTemplates := {
    0x0008, // Sarlac
    0x002f  // Ent
};

Const DIFF_EINHORNBLUT  := 90;
Const DIFF_DRACHENBLUT  := 90;
Const DIFF_DAEMONENBLUT := 70;
Const DIFF_TROLLBLUT    := 50;
Const DIFF_BLUT         := 30;

Const ERR_BACKPACK    := 1;
Const ERR_BADMATERIAL := 2;

program BlutPhiole(who, phiole)
    SendSysMessagePergon(
        who, "Wählt eine Leiche, aus der Ihr Blut entnehmen wollt."
    );
    var corpse := Target(who, TGTOPT_CHECK_LOS);

    If (!corpse)
        SendSysMessagePergon(who, "Abbruch", "Abort");
        return;
    EndIf

    If (corpse.objtype != 0x2006)
        SendSysMessagePergon(who, "Das ist keine Leiche!");
        return;
    EndIf

    If (Distance(who, corpse) > 1)
        SendSysMessagePergon(who, "Ihr seid zu weit entfernt.");
        return;
    EndIf

    If (GetObjProperty(corpse, "Aderlass"))
        SendSysMessagePergon(
            who, "Aus dieser Leiche wurde bereits Blut extrahiert."
        );
        return;
    EndIf

    If (
        GetObjProperty(corpse, "Undead") ||
        GetObjProperty(corpse, PROP_SUMMONED) ||
        (corpse.corpsetype in SkelettLeichenTemplates) ||
        (corpse.corpsetype in KeinBlutTemplates)
    )
        SendSysMessagePergon(
            who,
            "Aus dieser Leiche ist kein einziger Tropfen Blut extrahierbar."
        );
        return;
    EndIf

    // Normales Blut als Standard
    var diff := DIFF_BLUT;
    var blut := UOBJ_BLUTFLAKON;

    // Standard ggf. ueberschreiben
    If (corpse.corpsetype in DrachenleichenTemplates)
        diff := DIFF_DRACHENBLUT;
        blut := UOBJ_DRACHENBLUT;
    ElseIf (corpse.corpsetype in DaemonenLeichenTemplates)
        diff := DIFF_DAEMONENBLUT;
        blut := UOBJ_DAEMONENBLUT;
    ElseIf (corpse.corpsetype in TrollLeichenTemplates)
        diff := DIFF_TROLLBLUT;
        blut := UOBJ_TROLLBLUT;
    ElseIf (corpse.corpsetype in EinhornLeichenTemplates)
        diff := DIFF_EINHORNBLUT;
        blut := UOBJ_EINHORNBLUT;
    EndIf

    // bei polymorphter (Player-)Leiche gibt es nur normales Blut
    If (GetObjProperty(corpse, "spell_poly"))
        diff := DIFF_BLUT;
        blut := UOBJ_BLUTFLAKON;
    EndIf

    // Falls NPC => null
    var qual := 0;
    var vprops := GetPropertyItem(phiole);
    If (vprops.quality)
        qual := vprops.quality;
    EndIf

    // quality-modifier
    var qmod := ((qual-0.5)*0.15+0.5)/diff*40;
    // difficulty-modifier
    var dmod := cint(diff*(1.0-qmod));

    var Skill_Check_Ok := 0;
    // fuer die "kein Skill"-Kandidaten
    If ((dmod - GetSkillPergon(who, SKILLID_OBDUKTION) > 20.0))
        var chance := RandomInt(100);
        If (blut == UOBJ_BLUTFLAKON && chance < 10)
            // 10%ige Chance bei Blut
            Skill_Check_Ok := 1;
        ElseIf (chance < 2)
            // 2% sonst
            Skill_Check_Ok := 1;
        EndIf
    // hat man skilltechnisch ne Chance, wird halt die genutzt
    ElseIf (CheckSkillPergon(who, SKILLID_OBDUKTION, dmod, CInt(55+diff/2)))
        Skill_Check_Ok := 1;
    EndIf

    // ErrorCondition bit array
    var ErrCond := 0;

    // minderwertiges Glas bei Daemonen und höher ->
    //     30% Misserfolgschance
    If ((qual < 1.0) && (diff > DIFF_TROLLBLUT) && (RandomInt(100) < 30))
        SendSysMessagePergon(
            who, "Das giftige Blut lässt eure Phiole zerschmelzen!"
        );
        Skill_Check_Ok := 0;
        ErrCond := ErrCond | ERR_BADMATERIAL;
    EndIf

    If (Skill_Check_Ok)
        var bloodvial := CreateItemInBackpackPergon(who, blut, 1);
        If (!bloodvial)
            SendSysMessagePergon(
                who, "Ihr wart zwar erfolgreich, doch scheinbar ist "+
                "euer Backpack zu voll"
            );
            ErrCond := ErrCond | ERR_BACKPACK;
        Else
            var text;
            Case (blut)
            UOBJ_BLUTFLAKON:
                text := "Nach ein wenig Anstrengung legt ihr einen "+
                    "Blutflakon in Euren Rucksack.";
            UOBJ_TROLLBLUT:
                text := "Nach ein wenig Anstrengung legt ihr ein "+
                    "Trollblut in Euren Rucksack.";
            UOBJ_DAEMONENBLUT:
                text := "Nach ein wenig Anstrengung legt ihr ein "+
                    "Dämonenblut in Euren Rucksack.";
            UOBJ_DRACHENBLUT:
                text :=
                    "Geschafft! Ihr legt ein Drachenblut in Euren Rucksack.";
            UOBJ_EINHORNBLUT:
                text :=
                    "Geschafft! Ihr legt ein Einhornblut in Euren Rucksack.";
            EndCase
            SendSysMessagePergon(who, text);
        EndIf
    Else
        // Phiole ist schon zerschmolzen
        If(!(ErrCond & ERR_BADMATERIAL))
            SendSysMessagePergon(
                who, "Ihr seid über und über mit Blut bespritzt, "+
                "die Phiole ging euch verloren."
            );
        EndIf
    Endif

    SubtractAmount(phiole, 1);
    bluten(who, Corpse);
EndProgram

Function bluten(who, Leiche)
    // Leiche als angezapft markieren
    SetObjProperty(Leiche, "Aderlass", 1);
    // Taeter markieren für Forensic
    SetObjProperty(Leiche, EV_CHOPPER, who.name);

    // 'bisschen' Blut erzeugen
    var Set;
    If (Leiche.corpsetype in DrachenLeichenTemplates)
        Set := "DrachenBlutObduktion";
    ElseIf (Leiche.corpsetype in DaemonenLeichenTemplates)
        Set := "DaemonenBlutObduktion";
    ElseIf (Leiche.corpsetype in EinhornLeichenTemplates)
        Set := "EinhornBlutObduktion";
    Else
        Set := "BlutObduktion";
    EndIf

    var BlutSet := MakeSet(
        Struct {
            x := Leiche.x, y := Leiche.y, z := Leiche.z,
            realm := Leiche.realm
        }, Set, 1
    );
    Detach();
    ForEach item in (BlutSet[2])
        SleepMs(2);
        item.saveonexit := 0;
    EndForEach
    Sleep(180);
    DestroyItem(Leiche);
    Sleep(60);
    ForEach item in (BlutSet[2])
        SleepMs(2);
        DestroyItem(item);
    EndForEach
EndFunction
