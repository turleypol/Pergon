use uo;

program console_online()
    var names := {};
    foreach chr in EnumerateOnlineCharacters()
        names.append( chr.name );
    endforeach

    names.sort();

    var msg := "System Message: " + names.size() + " Character";
    if ( names.size() == 1 )
        msg += " is";
    else
        msg += "s are";
    endif
    print( msg + " online" );

    var namestr := "";
    foreach name in names
        var toadd := "";
        if (namestr == "")
            toadd := name;
        else
            toadd := ", " + name;
        endif
        if (len(namestr+toadd) < 80)     // it'll fit
            namestr += toadd;
        else
            print( namestr );
            namestr := name;
        endif
    endforeach
    if (namestr != "")
        print( namestr );
    endif


endprogram
