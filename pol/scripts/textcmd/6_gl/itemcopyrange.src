//////////////////////////////////////////////////////////////////
//
//   StaticsMove - Copy all Objects from Source to Dest Location
//
//     Author: Shinigami
//
//   Modifications:
//
//////////////////////////////////////////////////////////////////

/////////////////
// Bibliotheken
/////////////////

Use uo;
Use os;

/////////////
// Includes
/////////////

Include "include/errormsg";
Include "include/itemnpc";
Include "include/msgs";

///////////////
// Konstanten
///////////////

Const ERRORMSGS_GERMAN:=
  "{}StaticsMove: CopyRange aktiviert..."+
  "{}StaticsMove: CopyRange beendet."+
  "{}X1 muss kleiner-gleich X2 bzw. Y1 muss kleiner-gleich Y2 sein. Z1 muss kleiner-gleich Z2 sein!"+
  "{}%% darf nur 0 bis 4095 sein!"+
  "{}%% darf nur 0 bis 6143 sein!"+
  "{}%% darf nur -128 bis 127 sein!"+
  "{}Sie muessen 6 Paramter angeben: X1 Y1 Z1 X2 Y2 Z2"+
  "{}Die Objekte wurden kopiert."+
  "{}Waehlt bitte einen Quell-Bezugspunkt !"+
  "{}Waehlt bitte einen Ziel-Bezugspunkt !"+
  "{}Abbruch";
Const ERRORMSGS_ENGLISH:=
  "{}StaticsMove: CopyRange started..."+
  "{}StaticsMove: CopyRange done."+
  "{}X1 must be lower/equal X2 and Y1 must be lower/equal Y2 and Z1 must be lower/equal Z2!"+
  "{}%% must be between 0 and 4095!"+
  "{}%% must be between 0 and 6143!"+
  "{}%% must be between -128 and 127!"+
  "{}U've to enter 6 Paramters: X1 Y1 Z1 X2 Y2 Z2"+
  "{}Objects have been copied."+
  "{}Choose Source Point !"+
  "{}Choose Destination Point !"+
  "{}Cancel";

/////////////////
// Hauptprogram
/////////////////

Program TextCMD_StaticsCopy(who, params)
  Set_Script_Option(SCRIPTOPT_NO_RUNAWAY, 1);

  params:=SplitWords(params);
  If (params.size()==6)
    If ((0<=CInt(params[1])) And (CInt(params[1])<6144))
      If ((0<=CInt(params[2])) And (CInt(params[2])<4096))
        If ((-129<CInt(params[3])) And (CInt(params[3])<128))
          If ((0<=CInt(params[4])) And (CInt(params[4])<6144))
            If ((0<=CInt(params[5])) And (CInt(params[5])<4096))
              If ((-129<CInt(params[6])) And (CInt(params[6])<128))

                If ((CInt(params[1])<=CInt(params[4])) And (CInt(params[2])<=CInt(params[5])) And
                    (CInt(params[3])<=CInt(params[6])))
                  PrintErrorMessage(9, {}, who);

                  Var quellbezug:=Target(who);
                  If (quellbezug)
                    quellbezug:={quellbezug.x, quellbezug.y, quellbezug.z};

                    PrintErrorMessage(10, {}, who);

                    Var zielbezug:=Target(who);
                    If (zielbezug)
                      zielbezug:={zielbezug.x, zielbezug.y, zielbezug.z};

                      PrintErrorMessage(1);
                      CopyBox(who, quellbezug, zielbezug,
                              CInt(params[1]), CInt(params[2]), CInt(params[3]),
                              CInt(params[4]), CInt(params[5]), CInt(params[6]));
                      PrintErrorMessage(2);
                    Else
                      PrintErrorMessage(11, {}, who);
                    EndIf
                  Else
                    PrintErrorMessage(11, {}, who);
                  EndIf
                Else
                  PrintErrorMessage(3, {}, who);
                EndIf
              Else
                PrintErrorMessage(6, {"Z2"}, who);
              EndIf
            Else
              PrintErrorMessage(5, {"Y2"}, who);
            EndIf
          Else
            PrintErrorMessage(4, {"X2"}, who);
          EndIf
        Else
          PrintErrorMessage(6, {"Z1"}, who);
        EndIf
      Else
        PrintErrorMessage(5, {"Y1"}, who);
      EndIf
    Else
      PrintErrorMessage(4, {"X1"}, who);
    EndIf
  Else
    PrintErrorMessage(7, {}, who);
  EndIf
EndProgram

////////////////////////////////
// CopyBox - kopiert die Items
////////////////////////////////

Function CopyBox(who, quellbezug, zielbezug, x1, y1, z1, x2, y2, z2)
  Var deltax:=zielbezug[1]-quellbezug[1];
  Var deltay:=zielbezug[2]-quellbezug[2];
  Var deltaz:=zielbezug[3]-quellbezug[3];
  Var item;
  Var movable;

  ForEach object in ListObjectsInBox(x1, y1, z1, x2, y2, z2,who.realm)
    If (object.isa(POLCLASS_ITEM))
      item:= CreateItemAtLocationPergon(object.x+deltax, object.y+deltay, object.z+deltaz, object.objtype, 1,who.realm);
      If (item)
        item.decayat:=0;
        item.movable:=object.movable;
        item.color:=object.color;
      EndIf
    ElseIf (object.isa(POLCLASS_MOBILE))
      movable:=object.frozen;
      object.frozen:=0;
      MoveObjectToLocation(object, object.x+deltax, object.y+deltay, object.z+deltaz, object.realm, MOVEOBJECT_FORCELOCATION);
      object.frozen:=movable;
    EndIf
  EndForEach

  PrintErrorMessage(8, {}, who);
EndFunction
