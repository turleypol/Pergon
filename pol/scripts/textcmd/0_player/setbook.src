///////////////////////////////////////////////////////////////////////////
// Bookcase Spawning system  version .3
// designed by Syzygy, Zulu and Bishop Ebonhand

use uo;
use util;
include "include/msgs";

var layout := {
  "page 0                         ",
  "gumppic   0   0 2321           ",
  "text    150  14    0    0      ",
  "group 1                        ",
  "radio    40  60 1209 1210 0 1  ",
  "radio   100  60 1209 1210 0 2  ",
  "radio   167  60 1209 1210 0 3  ",
  "radio   220  60 1209 1210 0 4  ",
  "radio    40  15 1209 1210 1 5  ",
  "tilepic  48  60 4079           ",
  "tilepic 108  60 4080           ",
  "tilepic 175  60 4081           ",
  "tilepic 230  60 4082           ",
  "text     53  14    0    1      ",
  "button  206 100 2128 2129 1 0 9",
  "text     52 102    0    2      ",
  "text    115 102    0    3      ",
  "group 2                        ",
  "radio    40 105 1209 1210 1 6  ",
  "radio   100 105 1209 1210 0 7  "
};

var data := {
  "Graphik",
  "Zufall",
  "Schreibbar",
  "Schreibschutz"
};

Program test(who)
  If (!who.getprop(TYPBIBLIOTHEKAR) and who.cmdlevel < CMDLEVEL_HIGHGM)
    SendSysMessagePergon(who,
      "Keine Berechtigung", "Access denied"
    );
    return;
  EndIf

  SendSysMessagePergon(who, "Buch wählen!", "Select a book!");
  var book := Target(who, TGTOPT_NOCHECK_LOS);
  If (!book)
    SendSysMessagePergon(who, "Abbruch", "Abort");
    return;
  EndIf

  var res := SendDialogGump(who, layout, data);
  If (res[0] == 9)
    ForEach thing in (res.keys)
      Case (thing)
      1:
        book.graphic := 4079;
      2:
        book.graphic := 4080;
      3:
        book.graphic := 4081;
      4:
        book.graphic := 4082;
      5:
        RandomGraphic(book);
      6:
        EraseObjProperty(book, "nowrite");
        PrintTextAbovePrivatePergon(book, "schreibbar", "writable", who);
      7:
        SetObjProperty(book, "nowrite", 1);
        PrintTextAbovePrivatePergon(book, "nur lesbar", "read only", who);
      EndCase
    EndForEach
  Else
    SendSysMessagePergon(who, "Abbruch", "Abort");
  EndIf
EndProgram

Function RandomGraphic(book)
  Case (RandomInt(4) + 1)
  1:
    book.graphic := 4079;
  2:
    book.graphic := 4080;
  3:
    book.graphic := 4081;
  4:
    book.graphic := 4082;
  EndCase
EndFunction

// vim: sw=2 sts=2
