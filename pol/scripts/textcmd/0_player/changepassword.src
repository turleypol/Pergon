//////////////////////////////////////////////////////////////////////////////
// TextCMD ChangePassword - Passwoerter von Spielern aendern
//
// Author: Shinigami

use os;
use uo;
include ":mail:common";
include "include/logutil";
include "include/msgs";
include "include/pergonutil";
include "include/permissions";
include "include/server";
include "include/varutil";

Program TextCMD_ChangePassword(who, param)
  var options := GetOptions(param);

  If (who.cmdlevel >= CMDLEVEL_SEER and options["web"] == 1)
    ChangeWebPassword(who);
    return;
  EndIf

  If (AllowedToAccessPasswords(who))
    ChangeOthersPasswords(who, options["account"]);
    return;
  EndIf

  ChangeOwnPassword(who);
EndProgram

// eigenes Passwort aendern
Function ChangeOwnPassword(who) // {{{
  SendSysMessagePergon(who, "Das eigene Passwort ändern ...");

  var oldpassword := SendTextEntryGump(who,
    "Gebt bitte zunaechst euer altes Passwort ein!",
    TE_CANCEL_ENABLE, TE_STYLE_NORMAL, 80
  );
  If (!oldpassword)
    SendSysMessagePergon(who, "Abbruch", "Abort");
    return;
  EndIf

  // Ist der User auch berechtigt?
  If (!who.acct.checkpassword(oldpassword))
    SendSysMessagePergon(who, "Euer Passwort ist falsch! Abbruch");
    return;
  EndIf

  var password := SendTextEntryGump(who,
    "Gebt bitte das neue Passwort ein!",
    TE_CANCEL_ENABLE, TE_STYLE_NORMAL, 80
  );
  If (!password)
    SendSysMessagePergon(who, "Abbruch", "Abort");
    return;
  EndIf

  If (Len(password) <= 4)
    SendSysMessagePergon(who, "Euer Passwort ist zu kurz!");
    return;
  EndIf

  // Sollte kein Apostroph oder Anfuehrungszeichen enthalten
  If (password["'"] or password[CChr(34)])
    SendSysMessagePergon(who,
      "Vermeidet bitte Apostroph oder Anführungszeichen!"
    );
    return;
  EndIf

  who.acct.setpassword(password);
  SendSysMessagePergon(who,
    "Euer Passwort wurde gesetzt. Bedenkt bitte, dass es im Forum "+
    "bis zu 60 Minuten dauert, bevor es dort ebenfalls aktiv ist."
  );
  syslog(
    "PASSWORD: "+CharInfoStr(who)+" hat sein Passwort geaendert"
  );
EndFunction // }}}

// fremdes Passwort per Auswahlgump aendern
Function ChangeOthersPasswords(who, accountname) // {{{
  Set_Script_Option(SCRIPTOPT_NO_RUNAWAY, 1);

  // keine Auswahldialog, wenn ein Accountname uebergeben wird
  If (!accountname)
    // Einen Anfangsbuchstaben auswaehlen
    var buchstabe := ChooseAccountBuchstabe(who, FillInAccountBuchstaben());
    If (buchstabe == -1)
        SendSysMessagePergon(who, "Abbruch", "Abort");
        return;
    EndIf

    // Einen Account auswaehlen
    accountname := ChooseAccountName(who, FillInAccountNames(buchstabe));
    If (!accountname)
        SendSysMessagePergon(who, "Account "+accountname+" nicht gefunden");
        return;
    EndIf
  EndIf

  // Accountobjekt zum Accountnamen ermitteln
  var account := FindAccount(accountname);
  If (!account)
    SendSysMessagePergon(who, "Account "+accountname+" nicht gefunden");
    return;
  EndIf

  var password := SendTextEntryGump(who,
    "Gebt bitte das neue Passwort fuer '"+accountname+"' ein!",
    TE_CANCEL_ENABLE, TE_STYLE_NORMAL, 80, "Abbruch: Zufallspasswort"
  );
  var autogenerated := 0;
  If (!password)
    autogenerated := 1;
    password := GenRandomCode();
  EndIf

  account.setpassword(password);
  SendSysMessagePergon(who,
    "Für '"+accountname+"' wurde ein neues Passwort gesetzt."
  );
  syslog(
    "PASSWORD: "+CharInfoStr(who)+" hat ein neues Passwort fuer "+
    accountname+" gesetzt"
  );

  If (!autogenerated)
    // bei manuell erstellten Passwoertern wird gefragt
    If (!SendYesNoGump(who, "Spieler per Mail benachrichtigen?"))
      return;
    EndIf
  EndIf

  var result := SendMail(
    MADDR_STAFF, account.getprop(PLAYEREMAIL), "Pergon: Neues Passwort",
    array{
      "Seid gegruesst!",
      "",
      "Euer Passwort wurde vom Staff geaendert. Hier Eure Daten:",
      "",
      "Login-Name: "+account.name,
      "Passwort:   "+password,
      "",
      "Tip: Im Spiel könnt Ihr Euer Passwort mit .changepassword aendern.",
      "",
      "Gehabt Euch wohl!"
    }
  );
  If (!result)
    SendSysMessagePergon(who,
      "Mail nicht verschickt: "+result.errortext
    );
  EndIf

  SendSysMessagePergon(who,
    "Der Spieler wurde per Mail ueber sein Passwort benachrichtigt."
  );
EndFunction // }}}

// ein Extra-Passwort fuer das Webinterface festlegen
Function ChangeWebPassword(who) // {{{
  SendSysMessagePergon(who, "Ein Web-Passwort festlegen ...");

  var oldpassword := SendTextEntryGump(who,
    "Gebt bitte zunaechst euer normales Passwort ein!",
    TE_CANCEL_ENABLE, TE_STYLE_NORMAL, 80
  );
  If (!oldpassword)
    SendSysMessagePergon(who, "Abbruch", "Abort");
    return;
  EndIf

  // Ist der User auch berechtigt?
  If (!who.acct.checkpassword(oldpassword))
    SendSysMessagePergon(who, "Euer Passwort ist falsch! Abbruch");
    return;
  EndIf

  var password := SendTextEntryGump(who,
    "Gebt bitte das neue Web-Passwort ein!",
    TE_CANCEL_ENABLE, TE_STYLE_NORMAL, 80
  );
  If (password == 0)
    SendSysMessagePergon(who, "Abbruch", "Abort");
    return;
  EndIf
  If (password == "")
    SendSysMessagePergon(who,
      "Web-Passwort wird gelöscht, normales Passwort fuer Webzugang nutzen"
    );
    who.acct.eraseprop(ACCT_WEBPASS);
    return;
  EndIf

  who.acct.setprop(ACCT_WEBPASS, password);
  SendSysMessagePergon(who, "Web-Passwort gesetzt");
EndFunction // }}}
