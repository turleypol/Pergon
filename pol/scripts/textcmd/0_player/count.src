//////////////////////////////////////////////////////////////////
// TextCMD Count - Zaehlt Gegenstaende in Containern
//
//     Author: Bregg

//////////////////////////////////////////////////////////////////
// Modification
// $Log: not supported by cvs2svn $
// 29.10.2005 Turley: Da fehlte das .objtype bei den Haaren
// 30.07.2004 Turley: Haare und Baerte nicht mehr mitzaehlen

use uo;
include "include/itemnpc";
include "include/msgs";
include "include/server";

var haare := {
  // Mensch
  0x2004, 0x203b, 0x203c, 0x203d, 0x2044, 0x2045, 0x2046, 0x2047, 0x2048,
  0x2049, 0x204a,
  // Elf männlich extra Haare
  0x2fbf, 0x2fc0, 0x2fc1, 0x2fc2, 0x2fcd, 0x2fce, 0x2fcf, 0x2fd1,
  // Elf weiblich extra Haare
  0x2fcc, 0x2fd0,
  // Bärte
  0x2005, 0x203e, 0x203f, 0x2040, 0x2041, 0x204b, 0x204c, 0x204d
};

Program TextCMD_Count(me)
  SendSysMessagePergon(me, "Wählt einen Kontainer!");

  var use_on := target(me);
  If (!use_on)
    SendSysMessagePergon(me, "Zielen abgebrochen");
    return;
  EndIf

  If (!Accessible(me, use_on))
    SendSysMessagePergon(me, "Ihr kommt da nicht ran!");
    return;
  EndIf

  If (!(use_on.isa(POLCLASS_CONTAINER)))
    SendSysMessagePergon(me, "Das ist kein zulässiger Kontainer!");
    return;
  EndIf

  var i := 0;
  ForEach item in EnumerateItemsInContainer(use_on)
    If (!(item.objtype in haare))
      i += GetAmount(item);
    EndIf
    SleepMS(2);
  EndForEach

  If (i == 0)
    SendSysMessagePergon(me, "Dieser Kontainer ist leer.");
  ElseIf (i == 1)
    SendSysMessagePergon(me, "Dieser Kontainer enthält 1 Gegenstand.");
  Else
    SendSysMessagePergon(me, "Dieser Kontainer enthält " + i + " Gegenstände.");
  EndIf
EndProgram
