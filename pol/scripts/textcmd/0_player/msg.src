/////////////////////////////////////////////////////////////////////
// TextCMD Msg - Schickt eine Nachricht an einen Spieler
//
//     Usage: .msg [playername]
//              Sends a Message to a Player. If Player is not given
//              a Menu will open and prompt you to choose one.
//
//     based on Msg - TextCommand by Racalac
//
//     Author: Shinigami

/////////////
// Bibliotheken&Includes
/////////////
use os;
use uo;
include "include/msgs";
include "include/onlinesearch";
include "include/pergonstatistik";
include "include/quests";
include "include/server";

//////////////////////
// Gump-Definitionen
//////////////////////

var layout := {
  "page 0",                       // Seite 0 (Basisseite)
  "nodispose",
  "resizepic 10 10 2620 310 590",
  "gumppic 30 40 2621",
  "checkertrans 15 16 300 578",

  "text 30 19 40 0",
  "text 234 19 40 1"
};

var data := {"Spielername", "Nachricht"};

//////////////////
// Hauptprogramm
//////////////////

Program TextCMD_Msg(who, eingabe)
  If (who.squelched)
    SendSysMessagePergon(who, "Ihr seid verstummt.", "You are squelched.");
    return;
  EndIf

  If (eingabe)
    var empfaenger := SearchForOnlineCharacter(who, eingabe, 1);
    If (!empfaenger)
      SendSysMessagePergon(who,
        eingabe+" konnte nicht gefunden werden.",
        "Unable to find "+eingabe+"."
      );
      return;
    EndIf
    SendMessagePlayer(who, empfaenger);

  Else
    // Liste mit {char.name, char}
    var playerinfo :=
      FillInOnlineCharacterInfo(layout, data, who, DONT_LIST_ME);
    If (!playerinfo)
      SendSysMessagePergon(who,
        "Es ist gerade kein weiterer Character online.",
        "No other character is online."
      );
      return;
    EndIf

    var nummer := SendDialogGump(who, layout, data)[0];
    If (!nummer)
      SendSysMessagePergon(who, "Abbruch", "Abort");
      return;
    EndIf

    SendMessagePlayer(who, playerinfo[nummer][2]);
  EndIf
EndProgram
