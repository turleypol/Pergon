///////////////////////////////////////////////////////////////////////
//
//   TextCMD NoCarve - Schaltet bei NPCs das Häuten beim Tod an oder aus
//
//     Author: Shinigami
//
//   Modification
//     2005/05/23 Shinigami: MultiRange hinzugefuegt
//     21.10.2005 Fraggulus: Dateiname angepasst
//     04.02.2007 Turley:    Kopie von NoLoot für Häuten
//
///////////////////////////////////////////////////////////////////////

/////////////////
// Bibliotheken
/////////////////

Use uo;

/////////////
// Includes
/////////////

Include "include/msgs";

//////////////////
// Hauptprogramm
//////////////////

Program TextCMD_NoCarve(who, text)
  Var params:=SplitWords(text);
  Var range := CInt(params[1]);
  Var bLoot := CINT(params[2]);

  If (bLoot > 0)
    bLoot:=1;
    SendSysMessagePergon(who, "Häuten wird aktiviert.", "Activate Carve.");
  Else
    bLoot:=0;
    SendSysMessagePergon(who, "Häuten wird deaktiviert.", "Deactivate Carve.");
  EndIf

  If (range > 0)
    If (range > 10)
      range := 10;
    EndIf
    SendSysMessagePergon(who, "Wählt den zentralen NPC für den Umkreis von " + range + " Feldern aus!",
                              "Choose a central npc for the circumference of " + range + " fields!");
  Else
    range := 0;
    SendSysMessagePergon(who, "Wählt einen NPC aus!", "Choose an npc!");
  EndIf

  Var mobiles:=Target(who);
  If (mobiles.isa(POLCLASS_NPC))
    If (range)
      ForEach mobile in ListMobilesNearLocationEx(mobiles.x, mobiles.y, mobiles.z, range, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN,who.realm)
        If (mobile.isa(POLCLASS_NPC))
          NoCarve(who, mobile, bLoot);
        EndIf
        SleepMS(2);
      EndForEach
    Else
      NoCarve(who, mobiles, bLoot);
    EndIf
  Else
    SendSysMessagePergon(who, "Abbruch", "Abort");
  EndIf
EndProgram

//////////////////////////////////
// NoCarve - Schaltet das Flag um
//////////////////////////////////

Function NoCarve(who, mobile, bLoot)
  If (bLoot==0)
    If (!(GetObjProperty(mobile, "nocarve")))
      SetObjProperty(mobile, "nocarve", 1);
      PrintTextAbovePrivatePergon(mobile, "NoCarve", "NoCarve", who);
    EndIf
  Else
    If ((GetObjProperty(mobile, "nocarve")))
      EraseObjProperty(mobile, "nocarve");
      PrintTextAbovePrivatePergon(mobile, "Carve", "Carve", who);
    EndIf
  EndIf
EndFunction
