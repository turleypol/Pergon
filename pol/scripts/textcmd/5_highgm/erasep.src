use uo;
include "include/logutil";
include "include/modifyskill";
include "include/pergonutil";
include "include/permissions";
include "include/server";

Program TextCMD_EraseObjProperty(who, param)
  If (!AllowedToChangeCProps(who))
    SendSysMessagePergon(who,
      "Ihr seid nicht befugt, diesen Befehl zu benutzen!",
      "You are not authorized to use this command!"
    );
    return;
  EndIf

  If (!param)
    SendSysMessagePergon(who, "Ihr müsst eine Property angeben!");
    return;
  EndIf

  var changable := 1;

  If (param[1, 3] == SKILLWERTKENNUNG)
    var propnamepart := param;
    propnamepart[1, 3] := "";
    If (IsSkillNameDefinied(propnamepart))
      changable := 0;
    EndIf
  ElseIf (param[1, 5] == SKILLTYPKENNUNG)
    If (IGNORE_TYPES[" "+GetObjProperty(who, param)+" "])
      var propnamepart := param;
      propnamepart[1, 5] := "";
      If (IsSkillNameDefinied(propnamepart))
        changable := 0;
      EndIf
    EndIf
  ElseIf (IGNORE_PROPS_MODIFIY[" "+param+" "])
    changable := 0;
  EndIf

  // Ist der Zugriff auf die Properties gestattet?
  If (!changable)
    SendSysMessagePergon(who,
      "Properties dieser Art dürfen nicht direkt geändert werden!"
    );
    return;
  EndIf

  SendSysMessagePergon(who, "Wählt ein Objekt!");
  var tgt := Target(who, TGTOPT_NOCHECK_LOS);
  If (!tgt)
    SendSysMessagePergon(who,
      "Kein normales Item - Wählt ein Paperdoll-Item!"
    );
    tgt := TargetCoordinates(who);
    If (tgt.item)
      tgt := tgt.item;
    EndIf
  EndIf
  If (!tgt)
    SendSysMessagePergon(who, "Abbruch", "Abort");
    return;
  EndIf

  var value := GetObjProperty(tgt, param);
  If (value == error)
    SendSysMessagePergon(who, "Die Property '"+param+"' gibt es nicht!");
    return;
  EndIf

  EraseObjProperty(tgt, param);
  SendSysMessagePergon(who,
    "Die Property '"+param+"' mit Wert "+value+" ("+TypeOf(value)+
    ") wurde gelöscht!"
  );
  // Logging
  syslog(
    "GMCMD: "+CharInfoStr(who)+" loescht die Property "+param+" bei "+
    CharInfoStr(tgt, COORDS_REALM)
  );
EndProgram
